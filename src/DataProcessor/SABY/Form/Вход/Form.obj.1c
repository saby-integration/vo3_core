&НаКлиенте
Функция ОбновитьЗаголовок()
	ЭтаФорма.Заголовок = "Вход в " + Элементы.Сервер.СписокВыбора.НайтиПоЗначению(api_url);
КонецФункции

//События формы и элементов формы
&НаСервере
Процедура ПрочитатьНастройки() 
	МодульОбъекта = ПолучитьМодульОбъекта();
	context_param = МодульОбъекта.НастройкиПодключенияПрочитать();
	
	Если context_param <> Неопределено Тогда
		//Заполним элементы формы данными из настроек
		context_param.Свойство("password", password);
		context_param.Свойство("login", login);
		context_param.Свойство("api_url", api_url);
		context_param.Свойство("ConnectionId", ConnectionId);
		proxy			= Неопределено;
		Если context_param.Свойство("Proxy", proxy) Тогда
			Если proxy.Свойство("Protocol") Тогда
				ProxyProtocol	= proxy.Protocol;
			КонецЕсли;
			Если proxy.Свойство("Server") Тогда
				ProxyServer	= proxy.Server;
			КонецЕсли;
			Если proxy.Свойство("Port") Тогда
				ProxyPort	= proxy.Port;
			КонецЕсли;
			Если proxy.Свойство("User") Тогда
				ProxyLogin	= proxy.User;
			КонецЕсли;
			Если proxy.Свойство("Password") Тогда
				ProxyPassword	= proxy.Password;
			КонецЕсли;
		КонецЕсли;
	Иначе
		context_param = Новый Структура();
	КонецЕсли;
	
	Если Элементы.Сервер.СписокВыбора.НайтиПоЗначению(api_url) = Неопределено Тогда
		api_url = Элементы.Сервер.СписокВыбора.Получить(0).Значение;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Модуль = ПолучитьМодульОбъекта();
	Элементы.Сервер.СписокВыбора.Очистить();
	Для Каждого ЗаписьСервер Из Модуль.СписокСерверовSaby() Цикл
		Элементы.Сервер.СписокВыбора.Добавить("https://"+ЗаписьСервер.Значение, ЗаписьСервер.Представление);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	АвторизацияУспешна	= Ложь;
	ПрочитатьНастройки();
	ОбновитьЗаголовок();
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	ЭлементыФормочки.Ошибка.Заголовок = "";
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Элементы.ГруппаНастройкиПодключения.Скрыть();
КонецПроцедуры

&НаСервере
Процедура ОтправитьКодАвторизации(ДополнительныеПараметры)
	Модуль = ПолучитьМодульОбъекта();
	//Первая стадия двухфакторной авторизации
	ПараметрыМетода	= Новый Структура("Идентификатор", ДополнительныеПараметры.Идентификатор);
	res = Модуль.local_helper_exec_method(
		context_param,
		Новый Структура("url, method, params, auto_auth",
			context_param.api_url+"/auth/service/",
			ДополнительныеПараметры.МетодОтправкиКодаПодтверждения,
			ПараметрыМетода,
			Ложь,
		)
	);
КонецПроцедуры

&НаСервере
Процедура ВходПроверкаКодаСМС(ДополнительныеПараметры, Код)
	Модуль = ПолучитьМодульОбъекта();
	//Вторая стадия двухфакторной авторизации
	ПараметрыМетода	= Новый Структура("Идентификатор, Код", ДополнительныеПараметры.Идентификатор, Код);
	res = Модуль.local_helper_exec_method(
		context_param,
		Новый Структура("url, method, params, auto_auth",
			context_param.api_url+"/auth/service/",
			ДополнительныеПараметры.МетодПроверкиКодаИсключения,
			ПараметрыМетода,
			Ложь,
		)
	);
	context_param.Вставить("session", res["result"]);
	context_param.Вставить("session_begin", ТекущаяДата());
	context_param.Вставить("last_token_time", Неопределено);
	Модуль.НастройкиПодключенияЗаписать(context_param);

	Модуль.ПослеАутентификации(context_param);
	Модуль.ПолучитьСтатусВерсии(Истина);
КонецПроцедуры

&НаКлиенте
Процедура Войти(Команда)
	АвторизацияУспешна = Ложь;
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	Если НЕ ЗначениеЗаполнено(login) ИЛИ НЕ ЗначениеЗаполнено(password) Тогда
		ЭлементыФормочки.Ошибка.Заголовок = "Не указан логин или пароль";
		Возврат;
	КонецЕсли;
	context_param.Вставить("login", login);
	context_param.Вставить("password", password);
	context_param.Вставить("api_url", api_url);
	context_param.Вставить("ConnectionId", ConnectionId);
	context_param.Удалить("Proxy");
	Если Не ПустаяСтрока(ProxyProtocol) И  Не ПустаяСтрока(ProxyServer) И (ProxyPort > 0 И ProxyPort <= 65535) Тогда
		Proxy	= Новый Структура("Protocol, Server, Port, User, Password",
			ProxyProtocol, ProxyServer, ProxyPort, ProxyLogin, ProxyPassword);
		context_param.Вставить("Proxy", Proxy);
	КонецЕсли;
	Попытка
		ВойтиНаСервере(context_param);
		АвторизацияУспешна = Истина;
		ЭтаФорма.Закрыть(context_param);
	Исключение
		ИнфОбОшибке = ИнформацияОбОшибке();
		ОшибкаСтруктура = ExtExceptionAnalyse(ИнфОбОшибке);
		ExtExceptionToJournal(ОшибкаСтруктура);
		
		ДеталиОшибки = get_prop(ОшибкаСтруктура, "detail");
		Если ТипЗнч(ДеталиОшибки) = Тип("Соответствие") Тогда
			Если	(Врег(ДеталиОшибки["classid"]) = "{00000000-0000-0000-0000-1FA000001002}")
					И ТипЗнч(ДеталиОшибки["addinfo"]) <> Неопределено Тогда
				ПарметрыФормыВводаСМС	= Новый Структура;
				Для Каждого КлЗнч Из ДеталиОшибки["addinfo"] Цикл
					ПарметрыФормыВводаСМС.Вставить(КлЗнч.Ключ, КлЗнч.Значение);
				КонецЦикла;
				ОшибкаСтруктура.Вставить("detail", "");
				ОшибкаСтруктура.Вставить("message", "");
				Попытка
					context_param.Вставить("session", ПарметрыФормыВводаСМС.ИдентификаторСессии);
					ОтправитьКодАвторизации(ПарметрыФормыВводаСМС);
					
					ПодсказкаЧ1 = "";
					ПарметрыФормыВводаСМС.Свойство("Сообщение", ПодсказкаЧ1);
					ПодсказкаЧ2 = "";
					ПарметрыФормыВводаСМС.Свойство("Телефон", ПодсказкаЧ2);
					ЭлементыФормочки.ИнформацияПользователю.Заголовок = ПодсказкаЧ1 + " " + ПодсказкаЧ2;
					
					ЭлементыФормочки.ГруппаВводаЛогинаИПароля.Видимость		= Ложь;
					ЭлементыФормочки.ПодтверждениеКода.Видимость				= Истина;
					ЭлементыФормочки.ОтменитьПодтверждение.КнопкаПоУмолчанию	= Истина;
				Исключение
					context_param.Вставить("session", "");
					ИнфОбОшибке = ИнформацияОбОшибке();
					ОшибкаСтруктура = ExtExceptionAnalyse(ИнфОбОшибке);
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
		ЭлементыФормочки.Ошибка.Заголовок = ExtExceptionToMessage(ОшибкаСтруктура);
	КонецПопытки;
КонецПроцедуры

&НаСервере
Функция ВойтиНаСервере(context_param)
	Модуль = ПолучитьМодульОбъекта();
	Модуль.ВойтиНаСервере(context_param);
КонецФункции

&НаКлиенте
Процедура ПодтвердитьКод(Команда)
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	Попытка
		ВходПроверкаКодаСМС(ПарметрыФормыВводаСМС, code_sms);
		code_sms = "";
		ЭлементыФормочки.ГруппаВводаЛогинаИПароля.Видимость	= Истина;
		ЭлементыФормочки.ПодтверждениеКода.Видимость		= Ложь;
		ЭлементыФормочки.Войти.КнопкаПоУмолчанию			= Истина;
		Закрыть(context_param);
	Исключение
		code_sms = "";
		ИнфОбОшибке = ИнформацияОбОшибке();
		ОшибкаСтруктура = ExtExceptionAnalyse(ИнфОбОшибке);
		ЭлементыФормочки.Ошибка.Заголовок = ExtExceptionToMessage(ОшибкаСтруктура);
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьПодтверждение(Команда)
	code_sms = "";
	ПарметрыФормыВводаСМС = Неопределено;
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	ЭлементыФормочки.ГруппаВводаЛогинаИПароля.Видимость	= Истина;
	ЭлементыФормочки.ПодтверждениеКода.Видимость			= Ложь;
	ЭлементыФормочки.Войти.КнопкаПоУмолчанию				= Истина;
КонецПроцедуры

&НаКлиенте
Процедура СерверПриИзменении(Элемент)
	ОбновитьЗаголовок();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Оповестить("Saby_ЗакрытиеФормыАвторизации", Новый Структура("АвторизацияУспешна, ДальнейшееДействие", АвторизацияУспешна, Параметры.ДальнейшееДействие));
КонецПроцедуры

#Область include_core_base_Helpers_FormGetters
#КонецОбласти

#Область include_core_base_ФормаНастроекНастрйкиПроксиСервера
#КонецОбласти

#Область include_core_base_Helpers_РаботаСоСвойствамиСтруктуры
#КонецОбласти

#Область include_core_base_ExtException
#КонецОбласти
