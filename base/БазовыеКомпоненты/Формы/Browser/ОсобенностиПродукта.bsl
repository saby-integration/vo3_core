
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// только для обработки
	МодульОбъекта = МодульОбъекта();
	context_param = МодульОбъекта.НастройкиПодключенияПрочитать();
	ЭлемФормы = ПолучитьЭлементыФормыНаСервере();
	
	ДобавитьИнформациюОВерсии(ЭлемФормы.ВерсияПродукта);
	Заголовок = "";
	Параметры.Свойство("Заголовок", Заголовок);
	Заголовок = Заголовок +" "+ ЛокализацияНазваниеПродукта();
	АдресСтраницы	= "";
	Параметры.Свойство("АдресСтраницы", АдресСтраницы);
	АдресСтраницыССлужебнымиПараметрами = get_prop(context_param, "api_url");
	АдресСтраницыССлужебнымиПараметрами = СтрЗаменить(АдресСтраницыССлужебнымиПараметрами, "https://", "http://");
	
	Если Параметры.Свойство("context_param") Тогда
		// При авторизхации НастройкиПодключенияПрочитать может вернуть НЕОПРЕДЕЛЕНО, да и выбранный домен на форме авторизации отличается
		context_param = Параметры.context_param;
	КонецЕсли;
	web_interface = get_prop(get_prop(context_param, "public"), "web_interface"); 
	Если web_interface = "BrowserSabyPlugin" Тогда
		Префикс = "http://localhost:9100/?pool=ExtLongPollingProxy&method=ExtLongPollingProxy.UI&id=0&protocol=6&params=eyJwYXRoIjogImluZGV4MUMuaHRtbCJ9&target=";  
		АдресСтраницы = АдресLPP(АдресСтраницы, "lpp1c.", "sbis");
	ИначеЕсли web_interface = "BrowserSaby" Тогда
		service = get_prop(context_param, "service");
		Если ЗначениеЗаполнено(service) Тогда
			service = service + "-";
		Иначе
			service = "";
		КонецЕсли;
		Префикс = "https://" + service + "online.sbis.ru/long_polling_proxy/service/?pool=ExtLongPollingProxy&method=LongPollingProxy.UI&id=0&protocol=6&params=eyJwYXRoIjogImluZGV4MUMuaHRtbCJ9&target=";
		АдресСтраницы = АдресLPP(АдресСтраницы, "online-lppbl1c.", "saby");
	Иначе
		Префикс = "";
	КонецЕсли;
	АдресСтраницы = Префикс + АдресСтраницы;
КонецПроцедуры

Функция АдресLPP(Адрес, Префикс, Домен)
	Адрес = СтрЗаменить(Адрес, "ie-1c.saby", Префикс + Домен);
	Адрес = СтрЗаменить(Адрес, "ieg-1c.sbis", Префикс + Домен);
	Адрес = СтрЗаменить(Адрес, "ie-1c.setty", Префикс + "setty");
	Адрес = СтрЗаменить(Адрес, "online.saby", Префикс + Домен);
	Адрес = СтрЗаменить(Адрес, "online.sbis", Префикс + Домен);
	Возврат Адрес;
КонецФункции

&НаСервере
Процедура СформироватьАдресАддона()
	МодульОбъекта = МодульОбъекта();
	api_url = get_prop(context_param, "api_url", МодульОбъекта.ПолучитьСервисПоУмолчанию());
	
	connection_info = МодульОбъекта.API_ADDON_READSYSTEMINFO(Неопределено);		
	АдресСтраницы = api_url+"/integration_config/1CBlockly/page/";
	// АдресСтраницы = АдресСтраницы + "ПриемНаРаботу?";
	АдресСтраницы = АдресСтраницы + "КадровыйУчет?";
	АдресСтраницыССлужебнымиПараметрами = АдресСтраницы;
	Для Каждого Элемент из connection_info Цикл 
		Если Не ЗначениеЗаполнено(Элемент.Значение) Тогда
			Продолжить;
		КонецЕсли;
		АдресСтраницыССлужебнымиПараметрами = АдресСтраницыССлужебнымиПараметрами + "&"+Элемент.Ключ + "=" + Элемент.Значение; 
	КонецЦикла;
	СимволПараметра = "&";
	Если Прав(АдресСтраницыССлужебнымиПараметрами,1) = "?" ИЛИ
		Прав(АдресСтраницыССлужебнымиПараметрами,1) = "&" Тогда
		СимволПараметра = "";
	КонецЕсли;
	АдресСтраницыССлужебнымиПараметрами = АдресСтраницыССлужебнымиПараметрами +СимволПараметра+ "debug=1";
	АдресСтраницыССлужебнымиПараметрами = АдресСтраницыССлужебнымиПараметрами + "&nocheck=1";
	//todo избавиться от типа объекта	
	//АдресСтраницыССлужебнымиПараметрами = АдресСтраницы;
	АдресСтраницы = АдресСтраницыССлужебнымиПараметрами; // для отладки
	Параметры.Свойство("Заголовок", ЛокализацияНазваниеПродукта());
КонецПроцедуры

Функция УрлСКорректнымиСимволамиПеременных(Знач ИсходныйУРЛ)
	ИсходныйУРЛ = СтрЗаменить(ИсходныйУРЛ, "?&", "?");
	ИсходныйУРЛ = СтрЗаменить(ИсходныйУРЛ, "??", "?");
	ИсходныйУРЛ = СтрЗаменить(ИсходныйУРЛ, "&&", "&");
	Результат = ИсходныйУРЛ;
	НовыйУРЛМассивом = Новый Массив;
	
	// Привести URL к корректному виду, Первый перефикс параметров в URL должен быть "?"
	мУРЛ1гоУровня = СтрРазделитьПодстрокой(ИсходныйУРЛ, "://");
	
	Для Каждого СтрокаАдреса Из мУРЛ1гоУровня Цикл
		СтрокаАдреса = СтрЗаменить(СтрокаАдреса, "?", "&");
		НомерПервогоЗнВопр = Найти(СтрокаАдреса, "&");
		Если НомерПервогоЗнВопр > 0 Тогда
			СтрокаАдреса = Лев(СтрокаАдреса, НомерПервогоЗнВопр-1) +
							"?" + Сред(СтрокаАдреса, НомерПервогоЗнВопр+1);
		КонецЕсли;
		НовыйУРЛМассивом.Добавить(СтрокаАдреса);
	КонецЦикла;
	Результат = СтрСоединить82(НовыйУРЛМассивом, "://");
	Возврат Результат;
КонецФункции

&НаСервере
Процедура СформироватьАдресСоСлужебнымиПараметрамиНаСервере(Тикет=Неопределено)
	МодульОбъекта = МодульОбъекта();
	
		АдресСтраницыССлужебнымиПараметрами = АдресСтраницы;
	Если Не ЭтоАдресСБИС(АдресСтраницы) Тогда
		Возврат;
	КонецЕсли;
	РазрешитьЗакрытиеФормы = Истина;//Установить в лож когда будет открыт редактор BLOKLY
	Если Найти(АдресСтраницыССлужебнымиПараметрами,"?") = 0 Тогда
		АдресСтраницыССлужебнымиПараметрами = АдресСтраницыССлужебнымиПараметрами + "?";
	КонецЕсли;
	АдресСтраницыССлужебнымиПараметрами = АдресСтраницыССлужебнымиПараметрами + "&nocheck=1";
	Если ЗначениеЗаполнено(Тикет) Тогда  
		АдресСтраницыССлужебнымиПараметрами = АдресСтраницыССлужебнымиПараметрами + ?(ЗначениеЗаполнено(Тикет), "&ticket=" + Тикет, "");
		ВремяПредыдущегоЗапросаТокена = ТекущаяДата();
		context_param.Вставить("last_token_time", ВремяПредыдущегоЗапросаТокена);
		МодульОбъекта.НастройкиПодключенияЗаписать(context_param);	
	КонецЕсли;		
	АдресСтраницыССлужебнымиПараметрами = УрлСКорректнымиСимволамиПеременных(АдресСтраницыССлужебнымиПараметрами);
	ПолучитьЭлементыФормыНаСервере().АдресСтраницы.Подсказка = АдресСтраницыССлужебнымиПараметрами;
   	
КонецПроцедуры	

&НаКлиенте
Процедура СформироватьАдресСоСлужебнымиПараметрами()
	//60 секнд * 30 минут
	ПериодМеждуЗапрсамиТокенов = 60*15;
    Тикет=Неопределено;
	//Тикет получаем при открытии формы если нет других открытых форм браузера
	Если context_param <> Неопределено Тогда
		ВремяПредыдущегоЗапросаТокена = get_prop(context_param, "last_token_time");
		НеобходимТикет = Ложь;
		Если ВремяПредыдущегоЗапросаТокена = Неопределено Тогда
			НеобходимТикет = Истина;
			ВремяПредыдущегоЗапросаТокена = ТекущаяДата();
		КонецЕсли;
		Если ТекущаяДата() - ВремяПредыдущегоЗапросаТокена >= ПериодМеждуЗапрсамиТокенов  Тогда
			НеобходимТикет = Истина;
		КонецЕсли;
		
		Если НеобходимТикет Тогда
			Попытка
			   Тикет = ТранспортИнтеграции.ПолучитьТикетТекущейСессии( context_param ); // если сессия не живая, значит пользователь получит окно аунтентификации
			Исключение
				ИнфоОбОшибке = ИнформацияОбОшибке();
				СтруктураОшибки = ExtExceptionAnalyse(ИнфоОбОшибке);
			КонецПопытки;
		КонецЕсли;
		//Обновим время обращения за тикетом
	КонецЕсли;
	СформироватьАдресСоСлужебнымиПараметрамиНаСервере(Тикет);
КонецПроцедуры


&НаСервере
Функция ЭтоАдресСБИС(Адрес)
	Если Найти(Адрес, ".sbis.ru") > 0 ИЛИ  Найти(Адрес, ".saby.ru") > 0 ИЛИ  Найти(Адрес, ".setty.kz") > 0 ИЛИ  Лев(Адрес, 16) = "http://localhost" Тогда
		Возврат Истина;
	Иначе
		Сообщить("Браузер открывает только сервера "+ЛокализацияНазваниеПродукта()+" или localhost (" + Адрес + ")");
		Возврат Ложь;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "SabySignOut" Тогда
		Закрыть();
	ИначеЕсли ИмяСобытия = "Saby_ЗавершениеДлительнойОперации" Тогда
		Результат = ПодготовитьРезультат(Параметр);
		ВернутьРезультат(Результат);
	КонецЕсли;
КонецПроцедуры

