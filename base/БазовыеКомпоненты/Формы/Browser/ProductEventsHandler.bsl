
&НаКлиенте
Функция ОбработатьДействиеНаКлиенте(ИмяДействия, Параметры)
	ПараметрыВызова = DecodeXmlXdto(Параметры);
	Если ИмяДействия = "ADDON_OPENFORM" Тогда
		Результат = API_ADDON_OPENFORM(ПараметрыВызова);
	ИначеЕсли ИмяДействия = "ADDON_SETCONNECTIONINFO" Тогда
		Результат = API_ADDON_SETCONNECTIONINFO(ПараметрыВызова);
	ИначеЕсли ИмяДействия = "BLOCKLY_SAVE_INI" Тогда
		Результат = API_BLOCKLY_SAVE_INI(ПараметрыВызова);
	ИначеЕсли ИмяДействия = "CALC_OPERATION" Тогда
		Форма = ПолучитьФормуОбработки("ДлительнаяОперация", "", , ЭтаФорма);
		Форма.Команда = ПараметрыВызова;
		Форма.Открыть();
		Возврат Неопределено;
	ИначеЕсли ИмяДействия = "ADDON_ONAUTH" Тогда
		Результат = ОбработатьДействие(ИмяДействия, ПараметрыВызова);
		Оповестить("Saby_ЗакрытиеФормыАвторизации");
		ЭтаФорма.Закрыть(Истина);
		Возврат Результат;
	Иначе
		Возврат ОбработатьДействие(ИмяДействия, ПараметрыВызова);	
	КонецЕсли;
	Возврат ПодготовитьРезультат(Результат);
КонецФункции 

&НаСервере
Функция ОбработатьДействие(ИмяДействия, ПараметрыВызова)
	МодульОбъекта = ПолучитьМодульОбъекта();
	Если ИмяДействия = "BLOCKLY_COMMANDS" Тогда
		Результат = МодульОбъекта.API_BLOCKLY_COMMANDS(ПараметрыВызова);
	ИначеЕсли ИмяДействия = "BLOCKLY_RUN" Тогда		
		Результат = МодульОбъекта.API_BLOCKLY_RUN(ПараметрыВызова);
	ИначеЕсли ИмяДействия = "BLOCKLY_UPDATE_INI" Тогда
		Результат = МодульОбъекта.API_BLOCKLY_UPDATE_INI(ПараметрыВызова);
	ИначеЕсли ИмяДействия = "DOCUMENT_READ_STATUS" Тогда
		Результат = МодульОбъекта.API_DOCUMENT_READ_STATUS(ПараметрыВызова);
	ИначеЕсли ИмяДействия = "ADDON_READSYSTEMINFO" Тогда
		Результат = МодульОбъекта.API_ADDON_READSYSTEMINFO(ПараметрыВызова);
	ИначеЕсли ИмяДействия = "ADDON_ONAUTH" Тогда
		Результат = МодульОбъекта.API_ADDON_ONAUTH(ПараметрыВызова, context_param);
	ИначеЕсли ИмяДействия = "ADDON_BROWSERUPDATEURL" Тогда
		АдресСтраницы	= get_prop(ПараметрыВызова, "url", АдресСтраницы);
		Результат = Истина;
	Иначе
		ВызватьИсключение NewExtExceptionСтрока(,"Неизвестная команда", ИмяДействия);
	КонецЕсли;
	Возврат ПодготовитьРезультат(Результат);
КонецФункции

&НаКлиенте
Функция API_ADDON_OPENFORM(ПараметрыВызова)
	Ссылка = get_prop(ПараметрыВызова, "LINK");
	Если Ссылка = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Если ТипЗнч(Ссылка) = Тип("Соответствие") Тогда
		Ссылка = ПолучитьСсылкуПоИдИС(Ссылка["ИмяИС"], Ссылка["ИдИС"]);
	КонецЕсли;	
	ПоказатьЗначение(,Ссылка);
	Возврат Неопределено;
КонецФункции

&НаКлиенте
Функция API_ADDON_SETCONNECTIONINFO(ПараметрыВызова)
	СтатусВерсииПродукта = get_prop(ПараметрыВызова, "ProductVersionStatus");
	Если СтатусВерсииПродукта <> Неопределено Тогда	
		СтатусВерсии = ПолучитьСтатусВерсииНаФорме(Ложь, СтатусВерсииПродукта);
		ОбновитьИнформациюОВерсии(СтатусВерсии);
	КонецЕсли;
	
	
	ConnectionIn = get_prop(ПараметрыВызова, "ConnectionId");
	
	Возврат "";
КонецФункции

&НаКлиенте
Функция API_BLOCKLY_SAVE_INI(ПараметрыВызова)
	Попытка
		ПараметрыВызова["name"] = ?(Найти(ПараметрыВызова["name"], "Blockly_") = 1, ПараметрыВызова["name"], "Blockly_"+ПараметрыВызова["name"]);
		ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогСохраненияФайла.ПолноеИмяФайла = ПараметрыВызова["name"];
		ДиалогСохраненияФайла.Расширение = "xml"; 
		ДиалогСохраненияФайла.Фильтр = НСтр("ru='xml|*.xml'");
		ДиалогСохраненияФайла.Заголовок = НСтр("ru=’Выберите файл'");
		ДиалогСохраненияФайла.МножественныйВыбор = Ложь;
		Если ДиалогСохраненияФайла.Выбрать() Тогда 
			ПутьКФайлу = ДиалогСохраненияФайла.ПолноеИмяФайла;	
			ФайлИни = Новый ЗаписьТекста(ПутьКФайлу);
			ФайлИни.ЗаписатьСтроку(ПараметрыВызова["value"]);
			ФайлИни.Закрыть();
		КонецЕсли;
	Исключение
		ИнфОбОшибке = ИнформацияОбОшибке();
		ВызватьИсключение(NewExtExceptionСтрока(ИнфОбОшибке, "Ошибка сохранения ini", ПараметрыВызова["name"]));
	КонецПопытки;
	Возврат "";
КонецФункции

&НаКлиенте
Процедура ДобавитьВЛог(Тип, Команда, Данные)
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	Если НЕ ЭлементыФормочки.Лог.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	СейчасВМилисекундах = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Сейчас = Дата(1, 1, 1) + СейчасВМилисекундах/1000;
	СейчасБезМилисекунд = Дата(Год(Сейчас), Месяц(Сейчас), День(Сейчас), Час(Сейчас), Минута(Сейчас), Секунда(Сейчас))- Дата(1,1,1);
	Милисекунды = СейчасВМилисекундах - СейчасБезМилисекунд * 1000;
	Время = Формат(Сейчас, "ДФ='mm:ss.'") + Формат(Милисекунды, "ЧЦ=3; ЧВН=; ЧГ=0");
	
	ЗаписьЛога = Лог.Вставить(0);
	ЗаписьЛога.Тип = Тип;
	ЗаписьЛога.Время = Время;
	ЗаписьЛога.Команда = Команда;
	ЗаписьЛога.Данные = Данные;
КонецПроцедуры

&НаСервере
Функция ЭтоАдресСБИС(Адрес)
	Если Найти(Адрес, ".sbis.ru") > 0 ИЛИ  Найти(Адрес, ".saby.ru") > 0 ИЛИ  Лев(Адрес, 16) = "http://localhost" Тогда
		Возврат Истина;
	Иначе
		Сообщить("Браузер открывает только сервера СБИС или localhost (" + Адрес + ")");
		Возврат Ложь;
	КонецЕсли;
КонецФункции

