
&НаКлиенте
Процедура ОбновитьКэшЛокальныхСертификатов(ОбработчикРезультата, ВремяЖизниКэша = 60) Экспорт
	
	// читаем из хранилища
	КэшЛокальныхСертификатов = МодульФоновогоЗаданияСервер().КэшЛокальныхСертификатовПрочитать();
	Если ЗначениеЗаполнено(КэшЛокальныхСертификатов.ПоследнееОбновление)
			И КэшЛокальныхСертификатов.ПоследнееОбновление + ВремяЖизниКэша > ТекущаяДата() Тогда
		
		ВыполнитьОбработкуОповещения(ОбработчикРезультата, Истина); // Берем из кэша
		Возврат;
		
	КонецЕсли;
	
	КэшЛокальныхСертификатов["Отпечатки"].Очистить();
	КэшЛокальныхСертификатов["Алгоритмы"].Очистить();
	МодульФоновогоЗаданияСервер().КэшЛокальныхСертификатовЗаписать(КэшЛокальныхСертификатов);
	
	Контекст = Новый Структура;
	
	Контекст.Вставить("ОбработчикРезультата", ОбработчикРезультата);
	Контекст.Вставить("КэшЛокальныхСертификатов", КэшЛокальныхСертификатов);
	
	ОбработчикРезультата = Новый ОписаниеОповещения(
		"ОбновитьКэшЛокальныхСертификатовПослеНахожденияМенеджераКриптографии", МодульФоновогоЗаданияКлиент(), Контекст);
	НайтиМенеджерКриптографии(ОбработчикРезультата);
	
КонецПроцедуры  

&НаКлиенте
Процедура ОбновитьКэшЛокальныхСертификатовПослеНахожденияМенеджераКриптографии(МенеджерКриптографии,
		Контекст) Экспорт
	
	Если МенеджерКриптографии = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Контекст.ОбработчикРезультата, Неопределено); // Ошибка
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("МенеджерКриптографии", МенеджерКриптографии);
	
	ОбработчикРезультата = Новый ОписаниеОповещения(
		"ОбновитьКэшЛокальныхСертификатовПослеПолученияХранилищаСертификатов", МодульФоновогоЗаданияКлиент(), Контекст);
	
	ПолучитьХранилищеСертификатов(ОбработчикРезультата, МенеджерКриптографии);
	
КонецПроцедуры  

&НаКлиенте
Процедура ОбновитьКэшЛокальныхСертификатовПослеПолученияХранилищаСертификатов(ХранилищеСертификатовКриптографии,
		Контекст) Экспорт
	
	Если ХранилищеСертификатовКриптографии = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Контекст.ОбработчикРезультата, Неопределено); // Ошибка
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("ХранилищеСертификатовКриптографии", ХранилищеСертификатовКриптографии);
	
	ОбработчикРезультата = Новый ОписаниеОповещения(
		"ОбновитьКэшЛокальныхСертификатовЗавершение", МодульФоновогоЗаданияКлиент(), Контекст);
	ПолучитьВсеСертификаты(ОбработчикРезультата, ХранилищеСертификатовКриптографии);
	
КонецПроцедуры  

&НаКлиенте
Процедура ОбновитьКэшЛокальныхСертификатовЗавершение(Сертификаты, Контекст) Экспорт
	
	Если Сертификаты = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Контекст.ОбработчикРезультата, Неопределено); // Ошибка
		Возврат;
	КонецЕсли;
	
	КэшЛокальныхСертификатов = Контекст.КэшЛокальныхСертификатов;
	
    Для Каждого Серт из Сертификаты Цикл
		КэшЛокальныхСертификатов["Отпечатки"].Добавить(Серт.Отпечаток);
	КонецЦикла;
	Для Каждого Менеджер Из МенеджерыКриптографииПрочитать() Цикл
		КэшЛокальныхСертификатов["Алгоритмы"].Вставить(Менеджер.Ключ);
	КонецЦикла;
	КэшЛокальныхСертификатов["ПоследнееОбновление"] = ТекущаяДата();
	МодульФоновогоЗаданияСервер().КэшЛокальныхСертификатовЗаписать(КэшЛокальныхСертификатов);
	
	ВыполнитьОбработкуОповещения(Контекст.ОбработчикРезультата, Истина);
	
КонецПроцедуры  

