
&НаКлиенте
Процедура НайтиМенеджерКриптографии(ОбработчикРезультата, Алгоритм = Неопределено)
	
	// Получаем асинхронно
	Контекст = Новый Структура;
	Контекст.Вставить("Алгоритм", Алгоритм);
	Контекст.Вставить("ОбработчикРезультата", ОбработчикРезультата);
	
	Если МенеджерыКриптографииПрочитать() = Неопределено Тогда
		НайтиМенеджерКриптографииНачатьПоиск(Контекст);
	Иначе
		НайтиМенеджерКриптографииЗавершение(Контекст);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НайтиМенеджерКриптографииНачатьПоиск(Контекст)
	
	ОбработчикРезультата = Новый ОписаниеОповещения(
		"НайтиМенеджерКриптографииПослеСозданияМенеджеровКриптографии", МодульФоновогоЗаданияКлиент(), Контекст);
	СоздатьМенеджерыКриптографии(ОбработчикРезультата, Контекст.Алгоритм)
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиМенеджерКриптографииПослеСозданияМенеджеровКриптографии(Менеджеры, Контекст) Экспорт
	
	Если Менеджеры = Неопределено ИЛИ Менеджеры.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(Контекст.ОбработчикРезультата, Неопределено); // Ошибка
		Возврат;
	КонецЕсли;
	
	ЗначМенеджерыКриптографии = Новый Соответствие;
	Для Каждого Менеджер Из Менеджеры Цикл
		ЗначМенеджерыКриптографии.Вставить(Менеджер.Ключ, Менеджер.Значение);
	КонецЦикла;
	МенеджерыКриптографииУстановить(ЗначМенеджерыКриптографии);
	
	НайтиМенеджерКриптографииЗавершение(Контекст);

КонецПроцедуры

&НаКлиенте
Процедура НайтиМенеджерКриптографииЗавершение(Контекст)
	
	Результат = Неопределено;
	
	Если Контекст.Алгоритм = Неопределено Тогда
		ЗначМенеджерыКриптографии = МенеджерыКриптографииПрочитать();
		Для Каждого Элем Из ЗначМенеджерыКриптографии Цикл
			Результат = Элем.Значение;
			Прервать;
		КонецЦикла
	Иначе
		Результат = МенеджерыКриптографииПрочитать().Получить(Контекст.Алгоритм);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.ОбработчикРезультата, Результат);
	
КонецПроцедуры

