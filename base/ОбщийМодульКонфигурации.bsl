
Функция ОбщийМодульКонфигурации(ИмяМодуля)
	
	Попытка
		Модуль = ОбщийМодуль(ИмяМодуля);		
	Исключение
		Модуль = Неопределено;
	КонецПопытки;
	
	Возврат Модуль;
		
КонецФункции

Функция ОбщийМодуль(Имя)
	
	Если Метаданные.ОбщиеМодули.Найти(Имя) <> Неопределено Тогда
		// АПК:488-выкл ВычислитьВБезопасномРежиме не используется, чтобы избежать вызова ОбщийМодуль рекурсивно.
		УстановитьБезопасныйРежим(Истина);
		Модуль = Вычислить(Имя);
		// АПК:488-вкл
	ИначеЕсли СтрЧислоВхождений(Имя, ".") = 1 Тогда
		Возврат СерверныйМодульМенеджера(Имя);
	Иначе
		Модуль = Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Модуль) <> Тип("ОбщийМодуль") Тогда
		ТекстОшибки = "Общий модуль "+Имя+" не существует";
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат Модуль;
	
КонецФункции

// Возвращает серверный модуль менеджера по имени объекта.
//
// Параметры:
//  Имя - Строка - Имя модуля менеджера.
//
// Возвращаемое значение:
//   МодульМенеджера
//
Функция СерверныйМодульМенеджера(Имя)
	ОбъектНайден = Ложь;
	
	ЧастиИмени = СтрРазделить82(Имя, ".");
	Если ЧастиИмени.Количество() = 2 Тогда
		
		ИмяВида = ВРег(ЧастиИмени[0]);
		ИмяОбъекта = ЧастиИмени[1];
		
		ДопустимыеВиды = Новый Массив();
		ДопустимыеВиды.Добавить(ВРег("Константы"));
		ДопустимыеВиды.Добавить(ВРег("РегистрыСведений"));
		ДопустимыеВиды.Добавить(ВРег("РегистрыНакопления"));
		ДопустимыеВиды.Добавить(ВРег("РегистрыБухгалтерии"));
		ДопустимыеВиды.Добавить(ВРег("РегистрыРасчета"));
		ДопустимыеВиды.Добавить(ВРег("Справочники"));
		ДопустимыеВиды.Добавить(ВРег("Документы"));
		ДопустимыеВиды.Добавить(ВРег("Отчеты"));
		ДопустимыеВиды.Добавить(ВРег("Обработки"));
		ДопустимыеВиды.Добавить(ВРег("БизнесПроцессы"));
		ДопустимыеВиды.Добавить(ВРег("ЖурналыДокументов"));
		ДопустимыеВиды.Добавить(ВРег("Задачи"));
		ДопустимыеВиды.Добавить(ВРег("ПланыСчетов"));
		ДопустимыеВиды.Добавить(ВРег("ПланыОбмена"));
		ДопустимыеВиды.Добавить(ВРег("ПланыВидовХарактеристик"));
		ДопустимыеВиды.Добавить(ВРег("ПланыВидовРасчета"));
		
		Если ДопустимыеВиды.Найти(ИмяВида) <> Неопределено Тогда 
			Если Метаданные[ИмяВида].Найти(ИмяОбъекта) <> Неопределено Тогда
		    	ОбъектНайден = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ОбъектНайден Тогда
		ТекстОшибки = "Общий модуль "+Имя+" не существует";
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	// АПК:488-выкл ВычислитьВБезопасномРежиме не используется, чтобы избежать вызова ОбщийМодуль рекурсивно.
	УстановитьБезопасныйРежим(Истина);
	Модуль = Вычислить(Имя);
	// АПК:488-вкл
	
	Возврат Модуль;
КонецФункции

