
Процедура ПослеОбновления(ПараметрыИнициализации)
	//Заполним справочник состояний объектов
	Справочники.Saby_СостоянияОбъектов.НачальнаяИнициализация();
	//Создадим ДопСведение - "КЭДО"
	СоздатьДополнительноеСвойствоКЭДО();
	ПараметрыИнициализации.ОбщиеНастройки.Вставить("ВерсияРасширения", ПараметрыИнициализации.НомерТекущейВерсии);
	ОбщиеНастройкиЗаписать(ПараметрыИнициализации.ОбщиеНастройки);
	СброситьКэшНаСервере();
КонецПроцедуры

Процедура СоздатьДополнительноеСвойствоКЭДО()
	ИмяСвойства	= "КЭДО";
	УстановитьПривилегированныйРежим(Истина);
	НайденноеСвойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию( ИмяСвойства, Истина );
	Если НЕ ЗначениеЗаполнено(НайденноеСвойство) Тогда
		//
		НовПВХ = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.СоздатьЭлемент();
		ТипСвойства			= Новый ОписаниеТипов("Булево");
		НовПВХ.Наименование	= ИмяСвойства;
		НовПВХ.Заголовок	= ИмяСвойства;
		НовПВХ.Имя			= ИмяСвойства;
		НовПВХ.ТипЗначения	= ТипСвойства;
        НовПВХ.ЭтоДополнительноеСведение = Истина;        
        НовПВХ.Записать();
		НайденноеСвойство	= НовПВХ.Ссылка;
	КонецЕсли;
	
	//Теперь настроим отображение этого элемента в справочнике сотрудников
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НДРС.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НаборыДополнительныхРеквизитовИСведений КАК НДРС
		|ГДЕ
		|	НДРС.ИмяПредопределенногоНабора = ""Справочник_Сотрудники"" ";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	РазделИнтерфейса = Неопределено;
	Для Каждого СтрокаРЗ Из РезультатЗапроса Цикл
		РазделИнтерфейса = СтрокаРЗ.Ссылка;
	КонецЦикла;
	Если РазделИнтерфейса = Неопределено Тогда
		РазделИнтерфейса	= Справочники.НаборыДополнительныхРеквизитовИСведений.СоздатьЭлемент();
		РазделИнтерфейса.Наименование	= "Сотрудники";
		РазделИнтерфейса.Используется	= Истина;
		РазделИнтерфейса.ИмяПредопределенногоНабора	= "Справочник_Сотрудники";
		РазделИнтерфейса.Записать();
		РазделИнтерфейса = РазделИнтерфейса.Ссылка;
	КонецЕсли;
	РазделИнтерфейса	= РазделИнтерфейса.ПолучитьОбъект();
	мЭлементов = РазделИнтерфейса.ДополнительныеСведения.НайтиСтроки(Новый Структура("Свойство",НайденноеСвойство));
	Если мЭлементов.Количество() = 0 Тогда
		СтрокаДопСведений = РазделИнтерфейса.ДополнительныеСведения.Добавить();
		СтрокаДопСведений.Свойство						= НайденноеСвойство;
		СтрокаДопСведений.ПометкаУдаления				= Ложь;
		СтрокаДопСведений.ИмяПредопределенногоНабора	= "Справочник_Сотрудники";
	КонецЕсли;
	РазделИнтерфейса.КоличествоСведений	= РазделИнтерфейса.ДополнительныеСведения.Количество();
	РазделИнтерфейса.Записать();
	
	УстановитьПривилегированныйРежим(Истина);
	ИспользованиеСвИДопЗнч = Константы.ИспользоватьДополнительныеРеквизитыИСведения.СоздатьМенеджерЗначения();
	ИспользованиеСвИДопЗнч.Прочитать();
	ИспользованиеСвИДопЗнч.Значение = Истина;
	ИспользованиеСвИДопЗнч.Записать();
КонецПроцедуры

Процедура ВключениеФункциональныхОпций()
	УстановитьПривилегированныйРежим(Истина);
	МетаданныеРасширения = ПолучитьМетаданныеРасширения();
	Если МетаданныеРасширения <> Неопределено Тогда
		Для каждого ФО Из МетаданныеРасширения.ФункциональныеОпции Цикл
			Константы[ФО.Имя].Установить(Истина);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура СброситьКэшНаСервере()
	УстановитьПривилегированныйРежим(Истина);
	СервераСБИС		= СписокСерверовSaby();
	Для Каждого АдресСбис Из СервераСБИС Цикл
		ХранилищеОбщихНастроек.Сохранить("Saby", "Ini"+АдресСбис.Значение, Неопределено,,"Saby");
	КонецЦикла;
КонецПроцедуры

