
// Проверяет заполненость реквизита, если он отсутствует, используется значение по умолчанию
//
// Параметры:
//  СтруктураИсточник - Структура - в ней производим необходимые действия.
//  ИмяСвойства - Строка - Имя свойства.
//  ЗначениеПоУмолчанию - Произвольные - Значение по умолчанию.
//
// Возвращаемое значение:
//  Булево - заполнено ли указанное свойство
//
//DynamicDirective
Функция has_prop(СтруктураИсточник, ИмяСвойства, ЗначениеПоУмолчанию=Неопределено) Экспорт
	Возврат ЗначениеЗаполнено(get_prop(СтруктураИсточник, ИмяСвойства, ЗначениеПоУмолчанию));
КонецФункции

// Возвращает значение свойства в источнике. Если нет такого - возвращается значение по умолчанию
//
// Параметры:
//  СтруктураИсточник - Структура - в ней производим необходимые действия.
//  ИмяСвойства - Строка - Имя свойства.
//  ЗначениеПоУмолчанию - Произвольный - Значение по умолчанию.
//
// Возвращаемое значение:
//  Произвольный - значение реквизита или неопределено.
//
//DynamicDirective
Функция get_prop(СтруктураИсточник, ИмяСвойства, ЗначениеПоУмолчанию=Неопределено) Экспорт
	Если СтруктураИсточник = Неопределено Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	Значение = Неопределено;
	Если ТипЗнч(СтруктураИсточник) = Тип("Структура") И (НЕ СтруктураИсточник.Свойство(ИмяСвойства, Значение)) Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	Попытка
		Значение = СтруктураИсточник[ИмяСвойства];
	Исключение
		Возврат ЗначениеПоУмолчанию;
	КонецПопытки;
	Если Значение = Неопределено Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	Возврат Значение;
КонецФункции

// Устанавливает значение свойства в приемнике из источника, если свойство в приемнике есть.
// Может использоваться значение по умолчанию.
//
// Параметры:
//  СтруктураИсточник - Структура - в ней производим необходимые действия.
//  СтруктураНазначения - Структура - в ней производим необходимые действия.
//  ИмяСвойстваИсточник - Строка - Имя свойства в источнике.
//  ИмяСвойстваПриемник - Строка - Имя свойства в приемнике.
//  ЗначениеПоУмолчанию - Произвольный - Значение по умолчанию.
//
//DynamicDirective
Процедура set_prop(СтруктураИсточник, СтруктураНазначения, ИмяСвойстваИсточник, ИмяСвойстваПриемник,ЗначениеПоУмолчанию = Неопределено) Экспорт
	ЗначениеИсточник = Get_Prop(СтруктураИсточник, ИмяСвойстваИсточник, ЗначениеПоУмолчанию);
	Если ЗначениеИсточник <> Неопределено Тогда
		СтруктураНазначения.Вставить(ИмяСвойстваПриемник, ЗначениеИсточник);
	КонецЕсли;
КонецПроцедуры

