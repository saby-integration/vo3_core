Функция СтрокаТаблицыЗначенийВСтруктуру(СтрокаТаблицыЗначений) Экспорт
	
	Структура = Новый Структура;
	Для каждого Колонка Из СтрокаТаблицыЗначений.Владелец().Колонки Цикл
		Значение = СтрокаТаблицыЗначений[Колонка.Имя];
		Если ТипЗнч(Значение) = Тип("ТаблицаЗначений") Тогда  
			Значение = ТаблицаЗначенийВМассив(Значение);
		КонецЕсли;	
		Структура.Вставить(Колонка.Имя, Значение);
	КонецЦикла;
	
	Возврат Структура;
	
КонецФункции

Функция МассивСтруктурВТаблицуЗначений(Массив)
    Результат = Новый ТаблицаЗначений;
    Элемент = Массив[0];
	Для Каждого ЭлементСтруктуры Из Элемент Цикл
		Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Неопределено") Тогда
			Продолжить;
		КонецЕсли;
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ТипЗнч(ЭлементСтруктуры.Значение));
		//ОписаниеТипов = Новый ОписаниеТипов("Число");
		Результат.Колонки.Добавить(ЭлементСтруктуры.Ключ, Новый ОписаниеТипов(МассивТипов));
	КонецЦикла;
	Для Каждого Элемент Из Массив Цикл
		Если ТипЗнч(Элемент) = Тип("Неопределено") Тогда
			Продолжить;
		КонецЕсли;	
		ЗаполнитьЗначенияСвойств(Результат.Добавить(), Элемент);
	КонецЦикла;
	Возврат Результат; 
КонецФункции	

Функция МассивСоответствийВТаблицуЗначений(Массив) 
	Результат = Новый ТаблицаЗначений;
	Для Каждого Запись ИЗ Массив Цикл
		НоваяСтрока = Результат.Добавить(); 
		Для Каждого РеквизитЗаписи ИЗ Запись Цикл
			НазваниеКолонки = РеквизитЗаписи.Ключ;
			НазваниеКолонки = СтрЗаменить(НазваниеКолонки,"@","_");
			Если Результат.Колонки.Найти(НазваниеКолонки) = Неопределено Тогда 
				Результат.Колонки.Добавить(НазваниеКолонки);	
			КонецЕсли;
			НоваяСтрока[НазваниеКолонки] = РеквизитЗаписи.Значение;	
		КонецЦикла;	
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция МассивВТаблицуЗначений(Массив) Экспорт
	
	Результат = Новый ТаблицаЗначений;
	
	Если Массив.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Элемент = Массив[0];	
	ТипЭлемента = ТипЗнч(Элемент);
	
	Если ТипЭлемента = Тип("Структура") Или ТипЭлемента = Тип("ФиксированнаяСтруктура") Тогда
		Результат = МассивСтруктурВТаблицуЗначений(Массив);
	КонецЕсли;	
	Если ТипЭлемента = Тип("Соответствие") Тогда
		Результат = МассивСоответствийВТаблицуЗначений(Массив);	
	КонецЕсли;
	Возврат Результат;
КонецФункции

Функция ТаблицаЗначенийВМассив(Таблица) Экспорт	
	Результат = Новый Массив;	
	Если ТипЗнч(Таблица) <> Тип("ТаблицаЗначений") Тогда 
		Возврат Результат;
	КонецЕсли;	
	Для Каждого СтрокаТаблицыЗначений Из Таблица Цикл
		НоваяСтрока = СтрокаТаблицыЗначенийВСтруктуру(СтрокаТаблицыЗначений);
		Результат.Добавить(НоваяСтрока);
	КонецЦикла;
	Возврат Результат;		
КонецФункции

