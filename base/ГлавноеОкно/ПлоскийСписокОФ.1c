
&НаСервере
Процедура ЗаполнитьКолонкиТаблицы() Экспорт
	ЭлемФормы = ПолучитьЭлементыФормыНаСервере();
	ИмяТаблицы = ТекущийРаздел["template"]; 
	
	ТабПоле = ЭлемФормы[ИмяТаблицы];
	ТабПоле.Значение.Очистить();
	ТабПоле.Колонки.Очистить();
	ТабПоле.Значение.Колонки.Очистить();
	Поле = ТабПоле.Колонки.Добавить(ИмяТаблицы + "Отмечен", "V");
	ТабПоле.Значение.Колонки.Добавить("Отмечен", Новый ОписаниеТипов("Булево"));
	Поле.УстановитьЭлементУправления(Тип("Флажок"));
	Поле.ДанныеФлажка = "Отмечен";
	Поле.РежимРедактирования = РежимРедактированияКолонки.Непосредственно;
	Поле.Ширина = 4;
	Поле.ИзменениеРазмера = ИзменениеРазмераКолонки.НеИзменять;
	КвалификаторыСтроки = Новый КвалификаторыСтроки();
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка", ,КвалификаторыСтроки);
	Для Каждого Колонка Из ТекущийРаздел.ПараметрыОтображения["columns"] Цикл
		ОписаниеТипа = ОписаниеТипаСтрока;
		Если get_prop(Колонка, "Тип") <> Неопределено Тогда
			ОписаниеТипа = Новый ОписаниеТипов(Колонка["Тип"]);
		КонецЕсли;
		Если get_prop(Колонка, "Видимость") = Истина Тогда
			Поле = ТабПоле.Колонки.Добавить(ИмяТаблицы + Колонка["Имя"], Колонка["Название"]);
			Поле.Данные = Колонка["Имя"];
			Для Каждого СвойствоПоля Из Колонка["Свойства"] Цикл
				Поле[СвойствоПоля.Ключ] = СвойствоПоля.Значение;
			КонецЦикла;
			Если get_prop(Колонка, "ЗагрузитьЕще") = Истина Тогда
				Поле = ТабПоле.Колонки.Добавить(ИмяТаблицы + "ЗагрузитьЕще", "Загрузить еще");
				Поле.Данные = "ЗагрузитьЕще";
				Поле.Положение = ПоложениеКолонки.НаСледующейСтроке;
				Поле.Гиперссылка = Истина;
				Поле.ОтображатьВШапке = Ложь;
				Поле.ЦветТекстаПоля = Новый Цвет(0, 85, 187);
				ТабПоле.Значение.Колонки.Добавить("ЗагрузитьЕще", ОписаниеТипа);
			КонецЕсли;
			Картинка = get_prop(Колонка, "КартинкиСтрок");
			Если Картинка <> Неопределено Тогда
				Поле.КартинкиСтрок = Новый Картинка(ПолучитьМодульОбъекта().ПолучитьМакет(Картинка), Истина);
				Поле.ЭлементУправления.ТипЗначения = ОписаниеТипа;
			Конецесли;
		КонецЕсли;
		ТабПоле.Значение.Колонки.Добавить(Колонка["Имя"], ОписаниеТипа);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписок() Экспорт
	ЭлемФормы = ПолучитьЭлементыФормыНаСервере();
	ИмяТаблицы = ТекущийРаздел["template"];
	
	ТабПоле = ЭлемФормы[ИмяТаблицы];
	Таблица = ТабПоле.Значение;
	ЗаполнитьДанныеСписка(Таблица);
КонецПроцедуры

Процедура ПлоскийСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	Если ДанныеСтроки.ЗагрузитьЕще = "" Тогда
		ОформлениеСтроки.Ячейки.ПлоскийСписокЗагрузитьЕще.Видимость = Ложь;
		ОформлениеСтроки.Ячейки.ПлоскийСписокСтатусСБИС.ОтображатьКартинку = Истина;
		ОформлениеСтроки.Ячейки.ПлоскийСписокСтатусСБИС.ИндексКартинки = ДанныеСтроки.СтатусСБИС;
		ОформлениеСтроки.Ячейки.ПлоскийСписокСтатусСБИС.ОтображатьТекст = Ложь;
	Иначе
		Для Каждого Ячейка Из ОформлениеСтроки.Ячейки Цикл
			Если Ячейка.Имя <> "ПлоскийСписокЗагрузитьЕще" Тогда
				Ячейка.Видимость = Ложь;	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

