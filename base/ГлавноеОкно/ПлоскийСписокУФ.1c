
&НаСервере
Процедура ЗаполнитьКолонкиТаблицы() Экспорт
	ЭлемФормы = ПолучитьЭлементыФормыНаСервере();
	ИмяТаблицы = ТекущийРаздел["template"];
	
	ТабПоле = ЭлемФормы[ИмяТаблицы];
	Таблица = ДанныеФормыВЗначение(ЭтаФорма[ИмяТаблицы], Тип("ТаблицаЗначений"));
	Таблица.Очистить();
	Таблица.Колонки.Очистить();
		
	СоздатьРеквизитыТаблицыФормы(Таблица, ИмяТаблицы);
	
	Если ТекущийРаздел.Свойство("ЭлементыФормы") Тогда
		Для Каждого Элем Из ТекущийРаздел.ЭлементыФормы Цикл
			ЭлемФормы.Удалить(ЭлемФормы.Найти(Элем));
		КонецЦикла;
		ТекущийРаздел.ЭлементыФормы.Очистить();
	Иначе
		ТекущийРаздел.Вставить("ЭлементыФормы", Новый Массив());
	КонецЕсли;
	
	Элем = ЭлемФормы.Добавить(ИмяТаблицы + "Отмечен", Тип("ПолеФормы"), ТабПоле);
	Элем.Вид = ВидПоляФормы.ПолеФлажка;
	Элем.ПутьКДанным = ИмяТаблицы + "." + "Отмечен";
	ТекущийРаздел.ЭлементыФормы.Добавить(ИмяТаблицы + "Отмечен");
	Для Каждого Колонка Из ТекущийРаздел.ПараметрыОтображения["columns"] Цикл
		Если get_prop(Колонка, "Видимость") = Истина Тогда
			Если get_prop(Колонка, "ЗагрузитьЕще") = Истина Тогда
				
				Родитель = ЭлемФормы.Добавить(ИмяТаблицы + Колонка["Имя"] + "ЗагрузитьЕще", Тип("ГруппаФормы"), ТабПоле);
				Родитель.Вид = ВидГруппыФормы.ГруппаКолонок;
				ТекущийРаздел.ЭлементыФормы.Добавить(ИмяТаблицы + Колонка["Имя"] + "ЗагрузитьЕще");
				Элем = ЭлемФормы.Добавить(ИмяТаблицы + "ЗагрузитьЕще", Тип("ПолеФормы"), Родитель);
				Элем.Вид = ВидПоляФормы.ПолеНадписи;
				Элем.ГиперссылкаЯчейки = Истина;
				Элем.ПутьКДанным = ИмяТаблицы + "." + "ЗагрузитьЕще";
				Элем.ОтображатьВШапке = Ложь;
				Элем.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
				Элем = ЭлемФормы.Добавить(ИмяТаблицы + Колонка["Имя"], Тип("ПолеФормы"), Родитель);
			Иначе	
				Элем = ЭлемФормы.Добавить(ИмяТаблицы + Колонка["Имя"], Тип("ПолеФормы"), ТабПоле);
				ТекущийРаздел.ЭлементыФормы.Добавить(ИмяТаблицы + Колонка["Имя"]);
			КонецЕсли;
			Элем.Вид = ВидПоляФормы.ПолеНадписи;
			Элем.ПутьКДанным = ИмяТаблицы + "." + Колонка["Имя"];
			Элем.Заголовок = Колонка["Название"];
			Для Каждого СвойствоПоля Из Колонка["Свойства"] Цикл
				Элем[СвойствоПоля.Ключ] = СвойствоПоля.Значение;
			КонецЦикла;
			Картинка = get_prop(Колонка, "КартинкиСтрок");
			Если Картинка <> Неопределено Тогда
				Элем.Вид = ВидПоляФормы.ПолеКартинки;
				Элем.КартинкаЗначений = ЭлемФормы["СлужебныйСписок" + Колонка["Имя"]].КартинкаЗначений;
			Конецесли;
		КонецЕсли;
	КонецЦикла;
	ЗначениеВДанныеФормы(Таблица, ЭтаФорма[ИмяТаблицы]);
	УстановитьУсловноеОформление();
КонецПроцедуры

Процедура СоздатьРеквизитыТаблицыФормы(Таблица, ИмяТаблицы)
	ТекущиеРеквизиты = ПолучитьРеквизиты(ИмяТаблицы);
	РеквизитыУдалить = Новый Массив();
	Для Каждого Рек Из ТекущиеРеквизиты Цикл
		РеквизитыУдалить.Добавить(Рек.Путь+"."+Рек.Имя);
	КонецЦикла;
	Реквизиты = Новый Массив();
	КвалификаторыСтроки = Новый КвалификаторыСтроки();
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка", ,КвалификаторыСтроки);
	Для Каждого Колонка Из ТекущийРаздел.ПараметрыОтображения["columns"] Цикл
		ОписаниеТипа = ОписаниеТипаСтрока;
		Тип = get_prop(Колонка, "Тип");
		Если Тип <> Неопределено Тогда
			Если Тип = "Соответствие" Тогда
				ОписаниеТипа = Новый ОписаниеТипов();
			Иначе
				ОписаниеТипа = Новый ОписаниеТипов(Колонка["Тип"]);
			КонецЕсли;
		КонецЕсли;
		Рек = Новый РеквизитФормы(Колонка["Имя"], ОписаниеТипа, ИмяТаблицы);
		Реквизиты.Добавить(Рек);
		Таблица.Колонки.Добавить(Колонка["Имя"], ОписаниеТипа);
		Если get_prop(Колонка, "ЗагрузитьЕще") = Истина Тогда
			Рек = Новый РеквизитФормы("ЗагрузитьЕще", ОписаниеТипаСтрока, ИмяТаблицы);
			Реквизиты.Добавить(Рек);
			Таблица.Колонки.Добавить("ЗагрузитьЕще", ОписаниеТипаСтрока);
		КонецЕсли;
	КонецЦикла;
	
	Рек = Новый РеквизитФормы("Отмечен", Новый ОписаниеТипов("Булево"), ИмяТаблицы);
	Реквизиты.Добавить(Рек);
	Таблица.Колонки.Добавить("Отмечен", Новый ОписаниеТипов("Булево"));
	
	ИзменитьРеквизиты(Реквизиты, РеквизитыУдалить);
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписок() Экспорт
	ЭлемФормы = ПолучитьЭлементыФормыНаСервере();
	ИмяТаблицы = ТекущийРаздел["template"];
	
	ТабПоле = ЭлемФормы[ИмяТаблицы];
	Таблица = ДанныеФормыВЗначение(ЭтаФорма[ИмяТаблицы], Тип("ТаблицаЗначений"));
	ЗаполнитьДанныеСписка(Таблица);
	ЗначениеВДанныеФормы(Таблица, ЭтаФорма[ИмяТаблицы]);
	ТабПоле.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = "СозданПрограммно";
	//Отбор
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПлоскийСписок.ЗагрузитьЕще");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	//Оформление
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//Поле оформления
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("ПлоскийСписокЗагрузитьЕще");
	
	//
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = "СозданПрограммно";
	//Отбор
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПлоскийСписок.ЗагрузитьЕще");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Заполнено;
	
	//Оформление
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//Поле оформления
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("ПлоскийСписокОтмечен");
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("ПлоскийСписокСтатусСБИС");
	
КонецПроцедуры

