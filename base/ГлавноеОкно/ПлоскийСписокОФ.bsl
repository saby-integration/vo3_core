
&НаСервере
Процедура ЗаполнитьКолонкиПлоскогоСписка() Экспорт
	ЭлемФормы = ПолучитьЭлементыФормыНаСервере();
	ИмяТаблицы = ТекущийРаздел["Шаблон"]; 
	
	ТабПоле = ЭлемФормы[ИмяТаблицы];
	ТабПоле.Значение.Очистить();
	ТабПоле.Колонки.Очистить();
	ТабПоле.Значение.Колонки.Очистить();
	Поле = ТабПоле.Колонки.Добавить(ИмяТаблицы + "Отмечен", "V");
	ТабПоле.Значение.Колонки.Добавить("Отмечен", Новый ОписаниеТипов("Булево"));
	Поле.УстановитьЭлементУправления(Тип("Флажок"));
	Поле.ДанныеФлажка = "Отмечен";
	Поле.РежимРедактирования = РежимРедактированияКолонки.Непосредственно;
	Поле.Ширина = 4;
	Поле.ИзменениеРазмера = ИзменениеРазмераКолонки.НеИзменять;
	Поле.ОтображатьИерархию = Истина;
	
	ЭлемФормы.ПлоскийСписок.РежимВыделения = РежимВыделенияТабличногоПоля.Одиночный;
	Если get_prop(ТекущийРаздел, "МножественныйВыбор") <> Истина Тогда
		Поле.Видимость = Ложь;
		ЭлемФормы.ОтметитьВсеПлоскийСписок.Видимость = Ложь;
		ЭлемФормы.ОтметитьВсеПлоскийСписок.Доступность = Ложь;
	КонецЕсли;
	
	КвалификаторыСтроки = Новый КвалификаторыСтроки();
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка", ,КвалификаторыСтроки);
	Для Каждого Колонка Из ТекущийРаздел.ПараметрыОтображения["Columns"] Цикл
		ОписаниеТипа = ОписаниеТипаСтрока;
		Если get_prop(Колонка, "Type") <> Неопределено Тогда
			ОписаниеТипа = ОписаниеТипаКолонки(Колонка["Type"]);
		КонецЕсли;
		ВидимостьКолонки = get_prop(Колонка, "Visibility", get_prop(Колонка, "VISIBLE", Ложь));
		ИмяКолонки = get_prop(Колонка, "Name", get_prop(Колонка, "NAME"));
		Если ВидимостьКолонки = Истина Тогда
			ШаблонКолонки = get_prop(Колонка, "TEMPLATE");
			Если ШаблонКолонки = "column_icon" Тогда
				Поле = ТабПоле.Колонки.Добавить(ИмяТаблицы + ИмяКолонки, Колонка["TITLE"]);
				Поле.Данные = ИмяКолонки;
				Поле.КартинкиСтрок = ЭлемФормы[Колонка["ICONS"]].Картинка;
				Поле.ЭлементУправления.ТипЗначения = Новый ОписаниеТипов("Число");
				Поле.Ширина = Колонка["WIDTH"];
				ОписаниеТипа = Новый ОписаниеТипов("Число");
			Иначе
				Поле = ТабПоле.Колонки.Добавить(ИмяТаблицы + ИмяКолонки, Колонка["Title"]);
				Поле.Данные = ИмяКолонки;
				Если get_prop(Колонка, "Properties") <> Неопределено Тогда
					Для Каждого СвойствоПоля Из Колонка["Properties"] Цикл
						Поле[СвойствоПоля.Ключ] = СвойствоПоля.Значение;
					КонецЦикла;
				КонецЕсли;
				Если get_prop(Колонка, "More") = Истина Тогда
					Поле = ТабПоле.Колонки.Добавить(ИмяТаблицы + "ЗагрузитьЕще", "Загрузить еще");
					Поле.Данные = "ЗагрузитьЕще";
					Поле.Положение = ПоложениеКолонки.НаСледующейСтроке;
					Поле.Гиперссылка = Истина;
					Поле.ОтображатьВШапке = Ложь;
					Поле.ЦветТекстаПоля = Новый Цвет(0, 85, 187);
					ТабПоле.Значение.Колонки.Добавить("ЗагрузитьЕще", ОписаниеТипа);
				КонецЕсли;
				Картинка = get_prop(Колонка, "RowIcon");
				Если Картинка <> Неопределено Тогда
					//Поле.КартинкиСтрок = Новый Картинка(ПолучитьМодульОбъекта().ПолучитьМакет(Картинка), Истина);
					Поле.КартинкиСтрок = ЭлемФормы[Картинка].Картинка;
					Поле.ЭлементУправления.ТипЗначения = ОписаниеТипа;
				Конецесли;
			Конецесли;
		КонецЕсли;
		ТабПоле.Значение.Колонки.Добавить(ИмяКолонки, ОписаниеТипа);
	КонецЦикла;
КонецПроцедуры

Процедура ПлоскийСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	Если ДанныеСтроки.ЗагрузитьЕще = "" Тогда
		ОформлениеСтроки.Ячейки.ПлоскийСписокЗагрузитьЕще.Видимость = Ложь;
		Попытка
			Если ОформлениеСтроки.Ячейки.Найти("ПлоскийСписокСтатусСБИС") <> Неопределено Тогда
				Если ДанныеСтроки.СтатусСБИС >= 0 Тогда
					ОформлениеСтроки.Ячейки.ПлоскийСписокСтатусСБИС.ОтображатьКартинку = Истина;
					ОформлениеСтроки.Ячейки.ПлоскийСписокСтатусСБИС.ИндексКартинки = ДанныеСтроки.СтатусСБИС;
				КонецЕсли;
				ОформлениеСтроки.Ячейки.ПлоскийСписокСтатусСБИС.ОтображатьТекст = Ложь;
			КонецЕсли;
		Исключение
		КонецПопытки;
		Попытка
			Если ОформлениеСтроки.Ячейки.Найти("ПлоскийСписокСтатус1С") <> Неопределено Тогда
				Если ДанныеСтроки.Статус1С >= 0 Тогда
					ОформлениеСтроки.Ячейки.ПлоскийСписокСтатус1С.ОтображатьКартинку = Истина;
					ОформлениеСтроки.Ячейки.ПлоскийСписокСтатус1С.ИндексКартинки = ДанныеСтроки.Статус1С;
				КонецЕсли;
				ОформлениеСтроки.Ячейки.ПлоскийСписокСтатус1С.ОтображатьТекст = Ложь;
			КонецЕсли;
		Исключение
		КонецПопытки
	Иначе
		Для Каждого Ячейка Из ОформлениеСтроки.Ячейки Цикл
			Если Ячейка.Имя <> "ПлоскийСписокЗагрузитьЕще" Тогда
				Ячейка.Видимость = Ложь;	
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

