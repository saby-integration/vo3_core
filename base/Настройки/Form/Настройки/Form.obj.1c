
&НаКлиенте
Перем ФормаОткрыта;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИзмененаДатаОбновленияСтатусов = Ложь;
	ЭлементыФормочки = ПолучитьЭлементыФормыНаСервере();
	ДобавитьИнформациюОВерсии(ЭлементыФормочки.ВерсияПродукта);
	
	ЭлементыФормочки.send_invitations.СписокВыбора.Очистить();
	ЭлементыФормочки.send_invitations.СписокВыбора.Добавить(Ложь, "Не отправлять приглашение");
	ЭлементыФормочки.send_invitations.СписокВыбора.Добавить(Истина, "Отправлять на online.saby.ru");
	ЭлементыФормочки.send_invitations.СписокВыбора.Добавить("my.saby.ru", "Отправлять на my.saby.ru");
	ЭлементыФормочки.send_invitations.СписокВыбора.Добавить("staff.saby.ru", "Отправлять на staff.saby.ru");
	
	МодульОбъекта	= ПолучитьМодульОбъекта();
	ОбщиеНастройки	= МодульОбъекта.ОбщиеНастройкиПрочитать();
	ПубличныеОбщиеНастройки	= get_prop(ОбщиеНастройки, "public");
	
	download_attachments_on_complete	= get_prop(ПубличныеОбщиеНастройки, "download_attachments_on_complete", Истина);
	download_attachments_on_update	= get_prop(ПубличныеОбщиеНастройки, "download_attachments_on_update", Истина);
	refresh_statuses			= get_prop(ПубличныеОбщиеНастройки, "refresh_statuses", Ложь);
	send_invitations			= get_prop(ПубличныеОбщиеНастройки, "send_invitations", Истина);
	run_docflow					= get_prop(ПубличныеОбщиеНастройки, "run_docflow", Истина);
	send_completed_documents	= get_prop(ПубличныеОбщиеНастройки, "send_completed_documents", Истина);
	pdf_attachments	= get_prop(ПубличныеОбщиеНастройки, "pdf_attachments", Ложь);
  	exchange_method	= get_prop(ПубличныеОбщиеНастройки, "exchange_method", "API");

	context_param	= МодульОбъекта.НастройкиПодключенияПрочитать();
	Тема			= get_prop(context_param, "Тема");
	advanced_log	= get_prop(context_param, "advanced_log", Ложь);
	ЗаполнитьДатуСобытияОбновленияСтатусов(context_param);
	
	ПрочитатьНастройкиПодсистемы("ЭТрН");
	ПрочитатьНастройкиПодсистемы("КЭДО");
	ПрочитатьНастройкиПодсистемы("ЭЛН");
	
	ВидимостьЭлементовКЭДО = Ложь; 
	Если Метаданные.ФункциональныеОпции.Найти("Saby_ИспользоватьКЭДО") <> Неопределено Тогда
        ВидимостьЭлементовКЭДО = ПолучитьФункциональнуюОпцию("Saby_ИспользоватьКЭДО");
	КонецЕсли;
	ЭлементыФормочки.грПриглашение.Видимость			= ВидимостьЭлементовКЭДО; 
	ЭлементыФормочки.run_docflow.Видимость				= ВидимостьЭлементовКЭДО; 
	ЭлементыФормочки.Вложения.Видимость					= ВидимостьЭлементовКЭДО; 
	ЭлементыФормочки.ОткрытьКадровыеДокументы.Видимость	= ВидимостьЭлементовКЭДО; 
	ЭлементыФормочки.ДекорацияЗадачиНативные.Видимость	= ВидимостьЭлементовКЭДО; 
	ЭлементыФормочки.ОткрытьЗадачи.Видимость			= ВидимостьЭлементовКЭДО; 
	ЭлементыФормочки.ОткрытьИсториюОбмена.Видимость		= ВидимостьЭлементовКЭДО; 
	
	ЭлементыФормочки.ДействиеСРегламентнымЗаданием.Заголовок = "Поиск регламентного задания";

	ПоказатьГруппуТранспорт();

КонецПроцедуры

&НаСервере
Функция ПолучитьКоличествоФункциональныхОпций()
	МодульОбъекта = ПолучитьМодульОбъекта();
	МетаданныеРасширения = МодульОбъекта.ПолучитьМетаданныеРасширения();
	КоличествоФО = 0;
	Если МетаданныеРасширения <> Неопределено Тогда 
		КоличествоФО = МетаданныеРасширения.ФункциональныеОпции.Количество();
	КонецЕсли;
	Возврат КоличествоФО;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ФормаОткрыта = Истина;
	ПолучитьАктуальностьВерсии();
	УстановитьЗаголовокПодсказкуКнопкиВыйти();
	КоличестовПопытокПолученияРЗ = 3;
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	ЭлементыФормочки.ДействиеСРегламентнымЗаданием.Заголовок = "Поиск регламентного задания...";
	ПослеОткрытия();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	ФормаОткрыта = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВыйтиНажатие(Элемент)
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	Если ЭлементыФормочки.Выйти.Заголовок = "Войти" Тогда
		ПроверкаВведенныхДанныхАутентификации = Новый ОписаниеОповещения("ОбработкаРезультатаЗакрытияФормыАвторизации",
																			ЭтаФорма);
		ОткрытьФормуОбработки("Вход", ЭтаФорма, ПроверкаВведенныхДанныхАутентификации);
	Иначе
		ВыйтиНаСервере();
		ПолучитьАктуальностьВерсии();
		УстановитьЗаголовокПодсказкуКнопкиВыйти();
		Сообщить("Сессия и пароль сброшены");
		Оповестить("SabySignOut");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ОбработкаСменыАккаунтаНаСервер(account_id)
	МодульОбъекта = ПолучитьМодульОбъекта();
	Результат = МодульОбъекта.local_helper_switch_account(Неопределено, account_id);
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		context_param = МодульОбъекта.ПроверитьНаличиеПараметровПодключения();
		МодульОбъекта.ПослеАутентификации(context_param);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаКлиенте 
Процедура ОбработкаСменыАккаунта(ВыбранныйЭлемент, ПараметрыОповещения) Экспорт
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИдентификаторСесии = ОбработкаСменыАккаунтаНаСервер(ВыбранныйЭлемент.Значение);
	Если ТипЗнч(ИдентификаторСесии) = Тип("Строка") Тогда
		ПоказатьОповещениеПользователя("Авторизация",,
			"Выполнен вход - " + ВыбранныйЭлемент.Представление,,
			СтатусОповещенияПользователя.Информация,
			Новый УникальныйИдентификатор);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СменитьАккаунтНажатие(Элемент)
	Попытка
		СписокАккаунтов = ПолучитьСписокАккаунтов();
	Исключение
		ИнфОбОшибке = ИнформацияОбОшибке();
		СтруктураОшибки = ExtExceptionAnalyse(ИнфОбОшибке);
		Картинка = КартинкаОшибка();
		ПоказатьОповещениеПользователя(
			"Ошибка",,СтруктураОшибки.message,
			Картинка,
			СтатусОповещенияПользователя.Важное);
		Возврат;
	КонецПопытки;
	
	Если СписокАккаунтов.Количество() < 2 Тогда
		ПоказатьОповещениеПользователя("Внимание",,
			"У вас один аккаунт. Смена невозможна.",,
			СтатусОповещенияПользователя.Информация,
			Новый УникальныйИдентификатор);
		Возврат;
	КонецЕсли;
	ОповещениеВыбораАккаунта = Новый ОписаниеОповещения("ОбработкаСменыАккаунта", ЭтаФорма);
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	ОткрытьВыборИзСписка(СписокАккаунтов, ОповещениеВыбораАккаунта, ЭлементыФормочки.ПереключитьАккаунт);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСопоставленияНажатие(Элемент)
	//Saby_КомандыОбменаДляФормыКлиент.ЗапуститьОповещениеПриНаличииСессии(Saby_КомандыОбменаДляФормыКлиент, "ЗагрузитьСопоставленияНа", Новый Структура());
	ЗапуститьОповещениеПриНаличииСессии("ЗагрузитьСопоставления", ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСопоставления(Результат = Неопределено, ДопПараметры = Неопределено) Экспорт
	connection_info = Неопределено;
	ТекстСообщения = "Загружены сопоставления по умолчанию.";
	Картинка = КартинкаУспешно();
	Попытка
		ЗагрузитьСопоставленияНаСервере(connection_info);
	Исключение
		ИнфоОбОшибке = ИнформацияОбОшибке();
		СтруктураОшибки = ExtExceptionAnalyse(ИнфоОбОшибке);
		Картинка = КартинкаОшибка();
		ТекстСообщения = СтруктураОшибки.message;
	КонецПопытки;
	ПоказатьОповещениеПользователя("Сопоставление завершено",,
		ТекстСообщения,
		Картинка,
		СтатусОповещенияПользователя.Важное,
		Новый УникальныйИдентификатор);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСопоставленияНаСервере(connection_info)
	//Дополнительная первоначальная ручная инициализация элементов справочника
	Справочники.Saby_СостоянияОбъектов.НачальнаяИнициализация();

	Модуль = ПолучитьМодульОбъекта();
	context_param = Модуль.НастройкиПодключенияПрочитать();
	system_info = Модуль.API_ADDON_READSYSTEMINFO(Неопределено);
	system_info.Вставить("ConnectionStateEvents", Новый Массив);
	system_info["ConnectionStateEvents"].Добавить("NewSystem");
	connection_info = Модуль.local_helper_init_connection(context_param, system_info);
	Модуль.ПриСозданииНовогоПодключения(connection_info);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРезультатаЗакрытияФормыАвторизации(Результат, Параметры) Экспорт
	Если Результат = Неопределено Тогда Возврат; КонецЕсли;
	ПолучитьАктуальностьВерсии();
	УстановитьЗаголовокПодсказкуКнопкиВыйти();
	//А вот тут бы проверить куда мы дальше хотим идти, если есть такие указания в - Параметры
	//Это для команд открытия формы браузера.
	Если ТипЗнч(Параметры) = Тип("Структура") Тогда
		ОповещениеМодуль = get_prop(Параметры,"Модуль");
		ОповещениеМетод = get_prop(Параметры,"Метод");
		Если ОповещениеМодуль = Неопределено ИЛИ ОповещениеМетод = Неопределено Тогда
			Возврат;
		КонецЕсли;
		МетодЗавершенияАвторизации = Новый ОписаниеОповещения(ОповещениеМетод, ОповещениеМодуль);
		ВыполнитьОбработкуОповещения(МетодЗавершенияАвторизации);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокПодсказкуКнопкиВыйти()
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	saby_param = ПолучитьСБИСПараметры();
	АвторизацияВыполнена = get_prop(saby_param, "session") <> Неопределено;
	Если АвторизацияВыполнена Тогда
		ЭлементыФормочки.Выйти.Заголовок = "Выйти";
		ЭлементыФормочки.Выйти.Подсказка = "Закрыть сессию в SABY";
	Иначе
		ЭлементыФормочки.Выйти.Заголовок = "Войти";
		ЭлементыФормочки.Выйти.Подсказка = "Открыть сессию в SABY";
	КонецЕсли;
	ЭлементыФормочки.ЛогинПользователя.Видимость	= АвторизацияВыполнена;
	ЭлементыФормочки.АдресСервера.Видимость			= АвторизацияВыполнена;
	ЭлементыФормочки.ЛогинПользователя.Заголовок	= get_prop(saby_param, "login");
	ЭлементыФормочки.АдресСервера.Заголовок			= get_prop(saby_param, "api_url");
	ЭлементыФормочки.ПереключитьАккаунт.Доступность = (ЭлементыФормочки.Выйти.Заголовок = "Выйти");
	ОбновитьДанныеНаФорме(ЭтаФорма);
КонецПроцедуры

&НаСервере
Функция ПолучитьСБИСПараметры()
	МодульОбъекта = ПолучитьМодульОбъекта();
	saby_param = МодульОбъекта.НастройкиПодключенияПрочитать();
	Возврат saby_param;
КонецФункции	

&НаСервере
Процедура ВыйтиНаСервере()
	МодульОбъекта = ПолучитьМодульОбъекта();
	saby_param = МодульОбъекта.НастройкиПодключенияПрочитать();
	Если saby_param <> Неопределено Тогда	
		saby_param.session = Неопределено;
		saby_param.password = Неопределено;
		МодульОбъекта.НастройкиПодключенияЗаписать(saby_param);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРедакторФайловНастроекНажатие(Элемент)
	ЗапуститьОповещениеПриНаличииСессии("ОткрытьРедакторФайловНастроек");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗадачиНажатие(Команда, ПараметрКоманды) Экспорт
	ЗапуститьОповещениеПриНаличииСессии("ОткрытьЗадачи");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКадровыеДокументыНажатие(Команда, ПараметрКоманды) Экспорт 
	ЗапуститьОповещениеПриНаличииСессии("ОткрытьКадровыеДокументы");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюОбменаНажатие(Элемент)
	ЗапуститьОповещениеПриНаличииСессии("ОткрытьИсториюОбмена");
КонецПроцедуры

&НаКлиенте
Процедура СброситьКэшНажатие(Элемент)
	СброситьКэшНаСервере();
	Сообщить("Общие настройки ини очищены");
КонецПроцедуры

&НаСервере
Процедура ЗаписатьОбщиеПараметрыНаСервере()
	МодульОбъекта = ПолучитьМодульОбъекта();
	ОбщиеНастройки	= МодульОбъекта.ОбщиеНастройкиПрочитать();
	ПубличныеОбщиеНастройки	= get_prop(ОбщиеНастройки, "public", Новый Структура);
	
	ПубличныеОбщиеНастройки.Вставить("download_attachments_on_complete",	download_attachments_on_complete);
	ПубличныеОбщиеНастройки.Вставить("download_attachments_on_update",		download_attachments_on_update);
	ПубличныеОбщиеНастройки.Вставить("refresh_statuses",					refresh_statuses);
	ПубличныеОбщиеНастройки.Вставить("run_docflow",							run_docflow);
	ПубличныеОбщиеНастройки.Вставить("send_invitations",					send_invitations);
	ПубличныеОбщиеНастройки.Вставить("send_completed_documents",			send_completed_documents);
	ПубличныеОбщиеНастройки.Вставить("pdf_attachments",						pdf_attachments);
	ПубличныеОбщиеНастройки.Вставить("exchange_method",						exchange_method);
	
	ОбщиеНастройки.Вставить("public", ПубличныеОбщиеНастройки);
	МодульОбъекта.ОбщиеНастройкиЗаписать(ОбщиеНастройки);  
	
	ЗаписатьНастройкиПодсистемы("ЭТрН");
	ЗаписатьНастройкиПодсистемы("КЭДО");
	ЗаписатьНастройкиПодсистемы("ЭЛН");
	ПоказатьГруппуТранспорт();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПараметрыПользователя();
	МодульОбъекта = ПолучитьМодульОбъекта();
	context_param = МодульОбъекта.НастройкиПодключенияПрочитать();
	Если context_param <> Неопределено Тогда 
		context_param.Вставить("advanced_log", advanced_log);
		context_param.Вставить("Тема", Тема);
		Если ИзмененаДатаОбновленияСтатусов Тогда 
			УстановитьДатуСобытияОбновленияСтатусов(context_param);
		КонецЕсли;
	КонецЕСли;
	МодульОбъекта.НастройкиПодключенияЗаписать(context_param);
КонецПроцедуры

&НаКлиенте
Процедура ДатаВремяУказателяСтатусаПриИзменении(Элемент)
	ИзмененаДатаОбновленияСтатусов = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДатуСобытияОбновленияСтатусов(context_param)
	ПоследнееСобытие = get_prop(context_param, "last_event");
	ДатаСобытияСтрокой =  get_prop(ПоследнееСобытие, "date", "01.01.0001 00.00.00");
	Если Не ПустаяСтрока(ДатаСобытияСтрокой) Тогда
		МодульОбъекта = ПолучитьМодульОбъекта();
		ЭлементыДатыСобытия = МодульОбъекта.СтрРазделить82(СтрЗаменить(ДатаСобытияСтрокой, " ", "."), ".");
		//В ином случае нам тут делать нечего, скорее всего будет произвольный набор данных
		//на коротый мы не расчитывали
		Если ЭлементыДатыСобытия.Количество() = 6 Тогда
			Попытка
				ДатаВремяУказателяСтатуса = Дата(
					ЭлементыДатыСобытия[2],
					ЭлементыДатыСобытия[1],
					ЭлементыДатыСобытия[0],
					ЭлементыДатыСобытия[3],
					ЭлементыДатыСобытия[4],
					ЭлементыДатыСобытия[5]);
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьДатуСобытияОбновленияСтатусов(context_param)
	УказателиНаСобытие = Новый Структура();
	Если ДатаВремяУказателяСтатуса <> Дата(1,1,1) Тогда
		УказателиНаСобытие = Новый Структура("date", Формат(ДатаВремяУказателяСтатуса, "ДФ='dd.MM.yyyy HH.mm.ss'"));
	КонецЕсли;
	context_param.Вставить("last_event", УказателиНаСобытие);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиПодсистемы(ИмяПодсистемы, ОтображатьФО = Истина)
	
	ЭлементыФормочки = ПолучитьЭлементыФормыНаСервере();
	
	ИмяФО = "Saby_Использовать" + ИмяПодсистемы;
	Если Метаданные.ФункциональныеОпции.Найти(ИмяФО) = Неопределено Тогда
		ЭлементыФормочки[ИмяФО].Видимость = Ложь;
	Иначе
		ЭтаФорма[ИмяФО] = Константы[ИмяФО].Получить();
	КонецЕсли;
	ЭлементыФормочки[ИмяФО].Видимость = ЭлементыФормочки[ИмяФО].Видимость И ОтображатьФО;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиПодсистемы(ИмяПодсистемы)
	
	ИмяФО = "Saby_Использовать" + ИмяПодсистемы;
	Если Метаданные.ФункциональныеОпции.Найти(ИмяФО) <> Неопределено Тогда
		Константы[ИмяФО].Установить(ЭтаФорма[ИмяФО]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТребуетсяВывестиСообщениеОНеобходимостиАвторизации()
	ОбработкаSABY = ПолучитьМодульОбъекта();
	Возврат ОбработкаSABY.ПроверитьНаличиеПараметровПодключения() = Неопределено;
КонецФункции

&НаКлиенте
Процедура ЗаписатьОбщиеПараметры()
	ЗаписатьПараметрыПользователя();
	ЗаписатьОбщиеПараметрыНаСервере();
	ОбновитьИнтерфейс(); //Обновление интерфейса после вкл/выкл функциональных опций
	Картинка = КартинкаОшибка();
	Если ТребуетсяВывестиСообщениеОНеобходимостиАвторизации() Тогда
		ПоказатьОповещениеПользователя(
			"Функционал SABY недоступен.",
			Неопределено,
			"Для работы необходимо авторизоваться.",
			Картинка,
			СтатусОповещенияПользователя.Важное);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда) 
	ЗаписатьОбщиеПараметры();
	Закрыть(Истина);
КонецПроцедуры 

&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьОбщиеПараметры();
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокАккаунтов()
	МодульОбъекта = ПолучитьМодульОбъекта();
	Результат = МодульОбъекта.local_helper_get_accoutslist(Неопределено);
	СписокАккаунтов	= Новый СписокЗначений;
	Если ТипЗнч(Результат) = Тип("Массив") Тогда
		Для Каждого ЭлементАккаунт Из Результат Цикл
			СписокАккаунтов.Добавить(get_prop(ЭлементАккаунт,"НомерАккаунта"), get_prop(ЭлементАккаунт,"НазваниеАккаунта"));
		КонецЦикла;
	КонецЕсли;
	Возврат СписокАккаунтов;
КонецФункции

&НаКлиенте
Процедура ДекорацияЗадачиНативныеНажатие(Элемент)
	ОткрытьФормуОбработки("Задачи", ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура РоботОперацииНажатие(Элемент)
	ОткрытьФормуОбработки("РоботСписок", ЭтаФорма);
КонецПроцедуры

&НаСервере
Функция ПроверкаНаличияКомпонентыSabyHttpsClient()
	Ответ = ИСТИНА;
	Попытка
		МодульОбъекта = ПолучитьМодульОбъекта();  
		Компонента = МодульОбъекта.ПодключитьКомпонентуSabyHttpsClient(Неопределено);
	Исключение 
		Ответ = ЛОЖЬ; 
		ИнфОбОшибке = ИнформацияОбОшибке();
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = ИнфОбОшибке.Описание;
		СообщениеПользователю.Сообщить();
	КонецПопытки;
	Возврат Ответ;
КонецФункции

&НаКлиенте
Процедура СпособОбменаПриИзменении(Элемент)
	Если exchange_method = "SabyHttpsClient" Тогда
		ЕстьКомпонентаSabyHttpsClient = ПроверкаНаличияКомпонентыSabyHttpsClient();
		Если НЕ ЕстьКомпонентаSabyHttpsClient Тогда
			exchange_method = "API";
		КонецЕсли;	
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметры, Источник)
	Если ИмяСобытия = "Saby_ЗакрытиеФормыАвторизации" Тогда
		//Параметры не важны
		//АвторизацияУспешна = get_prop(Параметры, "АвторизацияУспешна", Ложь);
		//ДальнейшееДействие = get_prop(Параметры, "ДальнейшееДействие", "");
		//Важно перерисовать форму
		ПолучитьАктуальностьВерсии(ФормаОткрыта);
		УстановитьЗаголовокПодсказкуКнопкиВыйти();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция КартинкаУспешно()
	Попытка
		Картинка = БиблиотекаКартинок["Saby_Успешно32"];
	Исключение
		Картинка = Неопределено;
	КонецПопытки;
	Если Картинка = Неопределено Тогда
		Попытка
			Картинка = БиблиотекаКартинок.Успешно32;
		Исключение
			Картинка = Новый Картинка; // Если нет нужных картинок, то пустая
		КонецПопытки;
	КонецЕсли;
	Возврат Картинка;
КонецФункции

&НаКлиенте
Функция КартинкаОшибка()
	Попытка
		Картинка = БиблиотекаКартинок["Saby_Ошибка32"];
	Исключение
		Картинка = Неопределено;
	КонецПопытки;
	Если Картинка = Неопределено Тогда
		Попытка
			Картинка = БиблиотекаКартинок.Ошибка32;
		Исключение
			Картинка = Новый Картинка; // Если нет нужных картинок, то пустая
		КонецПопытки;
	КонецЕсли;
	Возврат Картинка;
КонецФункции

Процедура ТемаПриИзменении(Элемент)
	ЗаписатьПараметрыПользователя();
КонецПроцедуры

#Область include_base_ОткрытьСтраницуИсторииИзменения
#КонецОбласти

#Область include_core_base_ПроверкаВерсии_НаФорме
#КонецОбласти

#Область include_core_base_Transport_МодульФормы 
#КонецОбласти

#Область include_core_base_Helpers_FormGetters
#КонецОбласти

#Область include_core_src_DataProcessor_SABY_ОсобенностиПлатформы_РаботаСЭлементамиФормы
#КонецОбласти

#Область include_core_base_Настройки_Form_Настройки_ОсобенностиПриложения
#КонецОбласти

#Область include_core_base_Настройки_Form_Настройки_ОсобенностиПродукта
#КонецОбласти

