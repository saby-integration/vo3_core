
&НаКлиенте
Перем ФормаОткрыта;

#Область include_core_base_locale_ЛокализацияДействиеПриОткрытии
#КонецОбласти

#Область include_core_base_Настройки_Form_Настройки_ОсобенностиПриложения
#КонецОбласти

#Область include_core_base_Настройки_Form_Настройки_ОсобенностиПродукта
#КонецОбласти

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИзмененаДатаОбновленияСтатусов = Ложь;
	ЭлементыФормочки = ПолучитьЭлементыФормыНаСервере();
	ДобавитьИнформациюОВерсии(ЭлементыФормочки.ВерсияПродукта);
	
	ЗаполнитьСпискиВыбораРеквизитов();
	ЗаполнитьРеквизитыИзПубличныхНастроек();
	ЗаполнитьРеквизитыИзНастроекПодключения();
		
	ПрочитатьНастройкиПодсистемы("ЭПД");
	ПрочитатьНастройкиПодсистемы("КЭДО");
	ПрочитатьНастройкиПодсистемы("ЭЛН");
	
	ВидимостьЭлементовКЭДО = Ложь; 
	Если Метаданные.ФункциональныеОпции.Найти("Saby_ИспользоватьКЭДО") <> Неопределено Тогда
        ВидимостьЭлементовКЭДО = ПолучитьФункциональнуюОпцию("Saby_ИспользоватьКЭДО");
	КонецЕсли;
	ЭлементыФормочки.грПриглашение.Видимость			= ВидимостьЭлементовКЭДО; 
	ЭлементыФормочки.run_docflow.Видимость				= ВидимостьЭлементовКЭДО;
	ЭлементыФормочки.kedo_mark.Видимость				= ВидимостьЭлементовКЭДО;
	ЭлементыФормочки.Вложения.Видимость					= ВидимостьЭлементовКЭДО; 
	ЭлементыФормочки.ОткрытьКадровыеДокументы.Видимость	= ВидимостьЭлементовКЭДО; 
	ЭлементыФормочки.ДекорацияЗадачиНативные.Видимость	= ВидимостьЭлементовКЭДО; 
	ЭлементыФормочки.ОткрытьЗадачи.Видимость			= ВидимостьЭлементовКЭДО; 
	ЭлементыФормочки.ОткрытьИсториюОбмена.Видимость		= ВидимостьЭлементовКЭДО; 
	
	ЭлементыФормочки.ДействиеСРегламентнымЗаданием.Заголовок = "Поиск регламентного задания";
	ЭтаФорма.Заголовок  = "Настройки "+ЛокализацияНазваниеПродукта();
	ЭлементыФормочки.download_attachments_on_update.Заголовок = "При загрузке документа из "+ЛокализацияНазваниеПродукта();
	ЭлементыФормочки.СтраницыSABY.Заголовок = "Cтраницы "+ЛокализацияНазваниеПродукта();
	ЭлементыФормочки.СтраницыSABY.Подсказка = "Cтраницы "+ЛокализацияНазваниеПродукта();
	ЭлементыФормочки.АдресСервера.Подсказка = "Адрес сервера "+ЛокализацияНазваниеПродукта();
	
	ПоказатьГруппуТранспорт();
	
	ЛокализацияДействиеПриОткрытии()
КонецПроцедуры

#Область include_core_base_Настройки_Form_Настройки_ЗаполнитьРеквизитыПриОткрытии
#КонецОбласти

&НаСервере
Функция ПолучитьКоличествоФункциональныхОпций()
	МодульОбъекта = ПолучитьМодульОбъекта();
	МетаданныеРасширения = МодульОбъекта.ПолучитьМетаданныеРасширения();
	КоличествоФО = 0;
	Если МетаданныеРасширения <> Неопределено Тогда 
		КоличествоФО = МетаданныеРасширения.ФункциональныеОпции.Количество();
	КонецЕсли;
	Возврат КоличествоФО;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ФормаОткрыта = Истина;
	ПолучитьАктуальностьВерсии();
	
	СтатусВерсии = ПолучитьСтатусВерсииНаФорме(Истина);
	ДанныеОшибки = get_prop(СтатусВерсии, "ДанныеОшибки");
	ОписаниеОшибки = get_prop(ДанныеОшибки, "detail");
	ТребуетсяОткрытьФормуАвторизации = НеобходимоВвестиКодПодтверждения(ОписаниеОшибки);
	Если ТребуетсяОткрытьФормуАвторизации Тогда
		//В фонме настроек мы просто разлогинимся
		ВыйтиНаСервере();
	КонецЕсли;
	
	УстановитьЗаголовокПодсказкуКнопкиВыйти();
	УстановитьДоступностьПоляТипОтправкиПриглашения();
	КоличестовПопытокПолученияРЗ = 3;
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	ЭлементыФормочки.ДействиеСРегламентнымЗаданием.Заголовок = "Поиск регламентного задания...";
	ПослеОткрытия();
КонецПроцедуры  


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	ФормаОткрыта = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	АвторизацияВыполнена = Неопределено;
	Если ТипЗнч(context_param) = Тип("Структура") Тогда
		context_param.Свойство("session", АвторизацияВыполнена);
	КонецЕсли;
	АвторизацияВыполнена =	АвторизацияВыполнена <> Неопределено;
	Если НЕ АвторизацияВыполнена Тогда
		Оповестить("Saby_ЗакрытиеФормыАвторизации",
			Новый Структура("АвторизацияУспешна, ДальнейшееДействие, ФормаНастроек", Ложь, ""));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыйтиНажатие(Элемент)
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	Если ЭлементыФормочки.Выйти.Заголовок = "Войти" Тогда
		ПроверкаВведенныхДанныхАутентификации = Новый ОписаниеОповещения("ОбработкаРезультатаЗакрытияФормыАвторизации",
																			ЭтаФорма);
		ОткрытьФормуОбработки("Вход",, ЭтаФорма,, ПроверкаВведенныхДанныхАутентификации);
	Иначе
		ВыйтиНаСервере();
		ПолучитьАктуальностьВерсии();
		УстановитьЗаголовокПодсказкуКнопкиВыйти();
		Сообщить("Сессия и пароль сброшены");
		Оповестить("SabySignOut");
		context_param = ПолучитьСБИСПараметры();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СменитьАккаунтНажатие(Элемент)
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	СменитьАккаунтВВыпадающемСписке(ЭлементыФормочки.ПереключитьАккаунт);
КонецПроцедуры

#Область include_core_base_Настройки_Form_Настройки_ЗагрузитьСопоставления
#КонецОбласти

#Область include_core_base_Настройки_Form_Настройки_ПриИзменении
#КонецОбласти

&НаКлиенте
Процедура ОбработкаРезультатаЗакрытияФормыАвторизации(Результат, Параметры) Экспорт
	Если Результат = Неопределено Тогда Возврат; КонецЕсли;
	ПолучитьАктуальностьВерсии();
	УстановитьЗаголовокПодсказкуКнопкиВыйти();
	//А вот тут бы проверить куда мы дальше хотим идти, если есть такие указания в - Параметры
	//Это для команд открытия формы браузера.
	Если ТипЗнч(Параметры) = Тип("Структура") Тогда
		ОповещениеМодуль = get_prop(Параметры,"Модуль");
		ОповещениеМетод = get_prop(Параметры,"Метод");
		Если ОповещениеМодуль = Неопределено ИЛИ ОповещениеМетод = Неопределено Тогда
			Возврат;
		КонецЕсли;
		МетодЗавершенияАвторизации = Новый ОписаниеОповещения(ОповещениеМетод, ОповещениеМодуль);
		ВыполнитьОбработкуОповещения(МетодЗавершенияАвторизации);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокПодсказкуКнопкиВыйти()
	ЭлементыФормочки = ПолучитьЭлементыФормы();
	saby_param = ПолучитьСБИСПараметры();
	АвторизацияВыполнена = get_prop(saby_param, "session") <> Неопределено;
	Если АвторизацияВыполнена Тогда
		ЭлементыФормочки.Выйти.Заголовок = "Выйти";
		ЭлементыФормочки.Выйти.Подсказка = "Закрыть сессию в "+ЛокализацияНазваниеПродукта();
	Иначе
		ЭлементыФормочки.Выйти.Заголовок = "Войти";
		ЭлементыФормочки.Выйти.Подсказка = "Открыть сессию в "+ЛокализацияНазваниеПродукта();
	КонецЕсли;

	ЭлементыФормочки.ЛогинПользователя.Видимость	= АвторизацияВыполнена;
	ЭлементыФормочки.АдресСервера.Видимость			= АвторизацияВыполнена;
	
	ЭлементыФормочки.ЛогинПользователя.Заголовок	= get_prop(saby_param, "user_fio");
	ЭлементыФормочки.АдресСервера.Заголовок			= get_prop(saby_param, "api_url");
	ЭлементыФормочки.ПереключитьАккаунт.Доступность = (ЭлементыФормочки.Выйти.Заголовок = "Выйти");
	ОбновитьДанныеНаФорме(ЭтаФорма);
КонецПроцедуры

#Область include_core_base_Настройки_ПрочитатьНастройки
#КонецОбласти

#Область include_core_base_Настройки_Form_Настройки_ОткрытьВебСтраницы
#КонецОбласти

#Область include_core_base_НалоговыйМониторинг
#КонецОбласти

#Область include_core_base_Настройки_Form_Настройки_СброситьКэш
#КонецОбласти

&НаСервере
Процедура ЗаписатьОбщиеПараметрыНаСервере()
	МодульОбъекта = ПолучитьМодульОбъекта();
	ОбщиеНастройки	= МодульОбъекта.ОбщиеНастройкиПрочитать();
	ПубличныеОбщиеНастройки	= get_prop(ОбщиеНастройки, "public", Новый Структура);
	
	ПубличныеОбщиеНастройки.Вставить("download_attachments_on_complete",	download_attachments_on_complete);
	ПубличныеОбщиеНастройки.Вставить("download_attachments_on_update",		download_attachments_on_update);
	ПубличныеОбщиеНастройки.Вставить("refresh_statuses",					refresh_statuses);
	ПубличныеОбщиеНастройки.Вставить("run_docflow",							run_docflow);
	ПубличныеОбщиеНастройки.Вставить("kedo_mark",							kedo_mark);
	ПубличныеОбщиеНастройки.Вставить("send_invitations",					send_invitations);
	ПубличныеОбщиеНастройки.Вставить("send_type",							send_type);
	ПубличныеОбщиеНастройки.Вставить("send_completed_documents",			send_completed_documents);
	ПубличныеОбщиеНастройки.Вставить("pdf_attachments",						pdf_attachments);
	ПубличныеОбщиеНастройки.Вставить("exchange_method",						exchange_method);
	ПубличныеОбщиеНастройки.Вставить("auto_update",							auto_update);
	ПубличныеОбщиеНастройки.Вставить("web_interface",						web_interface);
	
	ОбщиеНастройки.Вставить("public", ПубличныеОбщиеНастройки);
	МодульОбъекта.ОбщиеНастройкиЗаписать(ОбщиеНастройки);  
	
	ЗаписатьНастройкиПодсистемы("ЭПД");
	ЗаписатьНастройкиПодсистемы("КЭДО");
	ЗаписатьНастройкиПодсистемы("ЭЛН");
	ПоказатьГруппуТранспорт();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПараметрыПользователя();
	МодульОбъекта = ПолучитьМодульОбъекта();
	context_param = МодульОбъекта.НастройкиПодключенияПрочитать();
	Если context_param <> Неопределено Тогда 
		context_param.Вставить("advanced_log", advanced_log);
		context_param.Вставить("Тема", Тема);
		Если ИзмененаДатаОбновленияСтатусов Тогда 
			УстановитьДатуСобытияОбновленияСтатусов(context_param);
		КонецЕсли;
	КонецЕСли;
	МодульОбъекта.НастройкиПодключенияЗаписать(context_param);
КонецПроцедуры

&НаКлиенте
Процедура ДатаВремяУказателяСтатусаПриИзменении(Элемент)
	ИзмененаДатаОбновленияСтатусов = Истина;
КонецПроцедуры

&НаСервере
Процедура УстановитьДатуСобытияОбновленияСтатусов(context_param)
	УказателиНаСобытие = Новый Структура();
	Если ДатаВремяУказателяСтатуса <> Дата(1,1,1) Тогда
		УказателиНаСобытие = Новый Структура("date", Формат(ДатаВремяУказателяСтатуса, "ДФ='dd.MM.yyyy HH.mm.ss'"));
	КонецЕсли;
	context_param.Вставить("last_event", УказателиНаСобытие);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиПодсистемы(ИмяПодсистемы, ОтображатьФО = Истина)
	
	ЭлементыФормочки = ПолучитьЭлементыФормыНаСервере();
	
	ИмяФО = "Saby_Использовать" + ИмяПодсистемы;
	Если Метаданные.ФункциональныеОпции.Найти(ИмяФО) = Неопределено Тогда
		ЭлементыФормочки[ИмяФО].Видимость = Ложь;
	Иначе
		ЭтаФорма[ИмяФО] = Константы[ИмяФО].Получить();
	КонецЕсли;
	ЭлементыФормочки[ИмяФО].Видимость = ЭлементыФормочки[ИмяФО].Видимость И ОтображатьФО;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиПодсистемы(ИмяПодсистемы)
	
	ИмяФО = "Saby_Использовать" + ИмяПодсистемы;
	Если Метаданные.ФункциональныеОпции.Найти(ИмяФО) <> Неопределено Тогда
		Константы[ИмяФО].Установить(ЭтаФорма[ИмяФО]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТребуетсяВывестиСообщениеОНеобходимостиАвторизации()
	ОбработкаSABY = ПолучитьМодульОбъекта();
	Возврат ОбработкаSABY.ПроверитьНаличиеПараметровПодключения() = Неопределено;
КонецФункции

&НаКлиенте
Процедура ЗаписатьОбщиеПараметры()
	ЗаписатьПараметрыПользователя();
	ЗаписатьОбщиеПараметрыНаСервере();
	Оповестить("Saby_НастройкиИзменены");
	ОбновитьИнтерфейс(); //Обновление интерфейса после вкл/выкл функциональных опций
	Картинка = КартинкаОшибка();
	Если ТребуетсяВывестиСообщениеОНеобходимостиАвторизации() Тогда
		ПоказатьОповещениеПользователя(
			"Функционал "+ЛокализацияНазваниеПродукта()+" недоступен.",
			Неопределено,
			"Для работы необходимо авторизоваться.",
			Картинка,
			СтатусОповещенияПользователя.Важное);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда) 
	ЗаписатьОбщиеПараметры();
	Закрыть(Истина);
КонецПроцедуры 

&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьОбщиеПараметры();
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗадачиНативныеНажатие(Элемент)
	ОткрытьФормуОбработки("Задачи",, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура РоботОперацииНажатие(Элемент)
	ОткрытьФормуОбработки("РоботСписок",, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметры, Источник)
	Если ИмяСобытия = "Saby_ЗакрытиеФормыАвторизации" И ФормаОткрыта Тогда
		Если ТипЗнч(Параметры) = Тип("Структура") И Параметры.Свойство("ФормаНастроек") Тогда
			// Процесс закрытия формы, ни чего не нужно выполнять
			Возврат;
		КонецЕсли;
		//Параметры не важны
		//АвторизацияУспешна = get_prop(Параметры, "АвторизацияУспешна", Ложь);
		//ДальнейшееДействие = get_prop(Параметры, "ДальнейшееДействие", "");
		//Важно перерисовать форму
		ПолучитьАктуальностьВерсии(ФормаОткрыта);
		УстановитьЗаголовокПодсказкуКнопкиВыйти();
		context_param = ПолучитьСБИСПараметры();
	КонецЕсли;
КонецПроцедуры

#Область include_core_base_Helpers_Картинки
#КонецОбласти

#Область include_core_base_Авторизация_СменаАккаунта
#КонецОбласти

#Область include_core_base_Авторизация_Выход
#КонецОбласти

#Область include_base_ОткрытьСтраницуИсторииИзменения
#КонецОбласти

#Область include_core_base_ПроверкаВерсии_НаФорме
#КонецОбласти

#Область include_core_base_Авторизация_Form_Вход_НеобходимоВвестиКодПодтверждения
#КонецОбласти

#Область include_core_base_Транспорт_МодульФормы 
#КонецОбласти

#Область include_core_base_Helpers_FormGetters
#КонецОбласти

#Область include_core_base_ОсобенностиПлатформы_РаботаСЭлементамиФормы
#КонецОбласти

#Область include_core_base_ДлительныеОперации_ПодготовкаФормыОсобенностиПродукта
#КонецОбласти

#Область include_core_base_ФоновыеЗадания_МодульФоновогоЗаданияСервер
#КонецОбласти

#Область include_core_base_ФоновыеЗадания_МодульФоновогоЗаданияКлиент
#КонецОбласти

