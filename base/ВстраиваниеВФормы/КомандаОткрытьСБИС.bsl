
&НаКлиенте
Процедура ПриНажатииОткрытьВСБИСПолучитьUID( Команда, ПараметрКоманды ) Экспорт  
	
	context_params	= ПроверитьНаличиеПараметровПодключенияНаСервере(); 
	
	Если ТребуетсяАутентификация(context_params, Команда, ПараметрКоманды) Тогда
		Возврат;
	КонецЕсли;	
	
	мСсылок = ПолучитьСписокСсылокНаОбъектыКоманды( ПараметрКоманды );
	ПроверкаОбмена = ПроисходилЛиОбменДокументов( мСсылок.Источник);
	Если ПроверкаОбмена.ОбменаНебыло.Количество()  > 0 Тогда
		ДопПараметры		= Новый Структура("Данные, Форма", ПроверкаОбмена, ПараметрКоманды.Форма );
		ДействиеВопроса = Новый ОписаниеОповещения("ОтветНаВопросОтправитьДокументыВСБИС", ЭтотМодуль(), ДопПараметры);
		ПоказатьВопрос(ДействиеВопроса, 
			"Среди выбранных для открытия документов есть "+
			Формат(ПроверкаОбмена.ОбменаНебыло.Количество(), "ЧГ=0")+
			" которые в "+ЛокализацияНазваниеПродукта()+" не загружались. Загрузить их?",
			РежимДиалогаВопрос.ДаНетОтмена,0,КодВозвратаДиалога.Да,"Документ не найден в "+ЛокализацияНазваниеПродукта());	
	Иначе
		ОткрытьВСБИС_ПолучитьURL( ПроверкаОбмена.БылОбмен, ПараметрКоманды );
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросОтправитьДокументыВСБИС(Результат, Параметры) Экспорт // здесь, думаю, комм
	Если Результат = КодВозвратаДиалога.Да Тогда
		мСсылок = ПолучитьСписокСсылокНаОбъектыКоманды(Параметры.Данные.ОбменаНебыло);
		ОткрытьФормуОбработки("ЗагрузкаДокументов",мСсылок,Параметры.Форма); 
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		//Получим ссылки на обработанные документы и откроем их в окнах
		ОткрытьВСБИС_ПолучитьURL( Параметры.Данные.БылОбмен, Параметры );
	Иначе
		//При отмене как и полагается не будем ни чего делать
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтветНаВопросОтправитьВложенияВСБИС(Результат, Параметры) Экспорт // здесь, думаю, комм
	Если Результат = КодВозвратаДиалога.Да Тогда
		мСсылок = ПолучитьСписокСсылокНаОбъектыКоманды(Параметры.Данные.ОбменаНебыло);  
		ОткрытьФормуОбработки("ЗагрузкаДокументов",мСсылок,Параметры.Форма); 
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		//Получим ссылки на обработанные документы и откроем их в окнах
		ВыгрузитьВложенияИзСБИС( Параметры.Данные.БылОбмен, Параметры );
	Иначе
		//При отмене как и полагается не будем ни чего делать
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВСБИС_ПолучитьURL_ПослеАутентификации(Результат, Параметры) Экспорт 
	Если Результат <> Неопределено Тогда
		ОткрытьВСБИС_ПолучитьURL( Параметры.Источник, Параметры.ПараметрКоманды );
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВСБИС_ПолучитьURL( Источник, ПараметрКоманды ) Экспорт 
	ОшибкаВыполнения	= Ложь;
	Попытка
		context_params	= ПроверитьНаличиеПараметровПодключенияНаСервере(); 

		СсылкиНаДокументыВСБИС = ПолучитьСсылкиНаДокументыВИнтеграции( Источник );
	Исключение
		ОшибкаВыполнения	= Истина;
		ИнфоОбОшибке = ИнформацияОбОшибке();
		СтруктураОшибки = ExtExceptionAnalyse(ИнфоОбОшибке);
		Если СтруктураОшибки.type = "Unauthorized" Тогда
			//Открыть форму аутентификации
			ВходяшиеПараметры	= Новый Структура("Источник, ПараметрКоманды",Источник,ПараметрКоманды);
			ПроверкаВведенныхДанныхАутентификации = Новый ОписаниеОповещения("ОткрытьВСБИС_ПолучитьURL_ПослеАутентификации", ЭтотМодуль(), ВходяшиеПараметры);
			ОткрытьФормуОбработки("Вход", , , ,ПроверкаВведенныхДанныхАутентификации);	
			Возврат;
		Иначе
			СтруктураОшибки = ExtExceptionAnalyse(ИнфоОбОшибке);
			//Если эти параметры отсутствуею, то и в настройках параметров подключения находится или Неопределено
			//или пустая структура, значит необходимо вызвать окно авторизации
			Если	Найти(СтруктураОшибки.message, "api_url") > 0 ИЛИ
					Найти(СтруктураОшибки.message, "password") > 0 ИЛИ
					Найти(СтруктураОшибки.message, "login") > 0 ИЛИ
					Найти(СтруктураОшибки.message, "api") > 0 Тогда
				ВходяшиеПараметры	= Новый Структура("Источник, ПараметрКоманды",Источник,ПараметрКоманды);
				ПроверкаВведенныхДанныхАутентификации = Новый ОписаниеОповещения("ОткрытьВСБИС_ПолучитьURL_ПослеАутентификации", ЭтотМодуль(), ВходяшиеПараметры);
				ОткрытьФормуОбработки("Вход", , , ,ПроверкаВведенныхДанныхАутентификации); 
				Возврат;
			Иначе
				ПоказатьОповещениеПользователя("Ошибка",,СтруктураОшибки.message,БиблиотекаКартинок["Ошибка32"],СтатусОповещенияПользователя.Важное, Новый УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;
	КонецПопытки;
	
	Если	Не ОшибкаВыполнения И СсылкиНаДокументыВСБИС.ОбработаноСОшибкой.Количество()  > 0 Тогда
		ДопПараметры		= Новый Структура("Данные, Форма", СсылкиНаДокументыВСБИС.ОбработаноСОшибкой, ПараметрКоманды.Форма );
		ДействиеВопроса = Новый ОписаниеОповещения("ОтветНаВопросОтправитьДокументыВСБИС", ЭтотМодуль(), ДопПараметры);
		ПоказатьОповещениеПользователя(
			"Удалены документы в "+ЛокализацияНазваниеПродукта(),
			, 
			"Среди выбранных для открытия документов есть "+
			Формат(СсылкиНаДокументыВСБИС.ОбработаноСОшибкой.Количество(), "ЧГ=0")+
			" которые были удалены в "+ЛокализацияНазваниеПродукта()+". Информация о статусах этих документов была удалена.",
			КартинкаОшибка(),
			СтатусОповещенияПользователя.Важное, Новый УникальныйИдентификатор);
		ДопПараметры = Новый Структура("ПараметрКоманды", ПараметрКоманды);
		ОбновитьТабличнуюЧастьФормы( Неопределено, ДопПараметры );
	ИначеЕсли Не ОшибкаВыполнения Тогда
		ОткрытьВСБИС( СсылкиНаДокументыВСБИС.ОбработаноУспешно, ПараметрКоманды.Форма );
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВСБИС( МассивДокументовКОтправке, Форма ) Экспорт 
	Для Каждого СсылкаНаДокумент Из МассивДокументовКОтправке Цикл
		ПараметрыФормы = Новый Структура( "Заголовок, АдресСтраницы");
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, СсылкаНаДокумент);
		Уникальность = Новый УникальныйИдентификатор;
		Если МассивДокументовКОтправке.Количество() > 0 Тогда
			Уникальность = СокрЛП(get_prop(МассивДокументовКОтправке[0], "UID", Новый УникальныйИдентификатор));
		КонецЕсли;
		АдресДокумента = get_prop(СсылкаНаДокумент,"АдресСтраницы");
		АдресДокумента = ПолучитьПрямуюССылку(АдресДокумента);
		#Если ТолстыйКлиентОбычноеПриложение Тогда 
		ЗапуститьПриложение(АдресДокумента); 
		#Иначе	
		ПерейтиПоНавигационнойСсылке(АдресДокумента);	
		#КонецЕсли
	КонецЦикла;
КонецПроцедуры

